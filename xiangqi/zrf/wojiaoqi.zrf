(define step (
  (verify (in-zone? $2))
  $1 (verify not-friend?)
  (verify (in-zone? $2))
  add
))

(define step-pawn (
  $1 (verify not-friend?)
  add
))

(define step-2 (
  $1 (verify empty?)
  $2 (verify not-friend?)
  add
))

(define step-e (
  $1 (verify empty?)
  $1 (verify not-friend?)
  (verify (in-zone? $2))
  add
))

(define slide (
  $1 
  (while empty?
     add
     $1
  )
  (verify not-friend?)
  add
))

(define shoot (
  $1 
  (while empty?
     add
     $1
  )
  (verify not-empty?) $1
  (while empty? $1)
  (verify not-friend?)
  add
))

(game
   (title "WojiaoQi")
   (history "Copyright (c) 1890 Tong Xie Geng.")

   (players Red Black)
   (turn-order Red Black)

   (board
      (image "images/11x11.bmp")
      (grid
         (start-rectangle 4 5 46 47)
         (dimensions
             ("a/b/c/d/e/f/g/h/i/j/k" (45 0)) ; files
             ("11/10/9/8/7/6/5/4/3/2/1" (0 45)) ; ranks
         )
         (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                     (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
                     (nn -1 -1) (ss 1 1)
         )
      )
      (symmetry Black (n s) (s n) (w e) (e w) (nw se) (se nw) (ne sw) (sw ne) (nn ss) (ss nn))
      (kill-positions f6 a1 a2 k10 k11)
      (zone
         (name fortress)
         (players Red)
         (positions i1 j1 k1 i2 j2 k2 i3 j3 k3)
      )
      (zone
         (name fortress)
         (players Black)
         (positions a9 b9 c9 a10 b10 c10 a11 b11 c11)
      )
      (zone
         (name home)
         (players Red)
         (positions b1  c1  d1  e1  f1  g1  h1  i1  j1  k1
                    b2  c2  d2  e2  f2  g2  h2  i2  j2  k2
                        c3  d3  e3  f3  g3  h3  i3  j3  k3
                            d4  e4  f4  g4  h4  i4  j4  k4
                                e5  f5  g5  h5  i5  j5  k5
                                        g6  h6  i6  j6  k6
                                            h7  i7  j7  k7
                                                i8  j8  k8
                                                    j9  k9)
      )
      (zone
         (name home)
         (players Black)
         (positions a11 b11 c11 d11 e11 f11 g11 h11 i11 j11
                    a10 b10 c10 d10 e10 f10 g10 h10 i10 j10
                    a9  b9  c9  d9  e9  f9  g9  h9  i9 
                    a8  b8  c8  d8  e8  f8  g8  h8
                    a7  b7  c7  d7  e7  f7  g7
                    a6  b6  c6  d6  e6
                    a5  b5  c5  d5
                    a4  b4  c4
                    a3  b3)
      )
      (zone
         (name enemy)
         (players Black)
         (positions b1  c1  d1  e1  f1  g1  h1  i1  j1  k1
                    b2  c2  d2  e2  f2  g2  h2  i2  j2  k2
                        c3  d3  e3  f3  g3  h3  i3  j3  k3
                            d4  e4  f4  g4  h4  i4  j4  k4
                                e5  f5  g5  h5  i5  j5  k5
                                        g6  h6  i6  j6  k6
                                            h7  i7  j7  k7
                                                i8  j8  k8
                                                    j9  k9)
      )
      (zone
         (name enemy)
         (players Red)
         (positions a11 b11 c11 d11 e11 f11 g11 h11 i11 j11
                    a10 b10 c10 d10 e10 f10 g10 h10 i10 j10
                    a9  b9  c9  d9  e9  f9  g9  h9  i9 
                    a8  b8  c8  d8  e8  f8  g8  h8
                    a7  b7  c7  d7  e7  f7  g7
                    a6  b6  c6  d6  e6
                    a5  b5  c5  d5
                    a4  b4  c4
                    a3  b3)
      )
      (zone
         (name cross)
         (players Red)
         (positions i1 k1 j2 i3 k3)
      )
      (zone
         (name cross)
         (players Black)
         (positions a9 c9 b10 a11 c11)
      )
   )

   (piece
      (name Soldier)
      (image Red "images/rsoldier.bmp" 
             Black "images/bsoldier.bmp")
      (moves
         (step-pawn n) (step-pawn w) 
         (step s enemy) (step e enemy)
      )
   )
   (piece
      (name Horse)
      (image Red "images/rhorse.bmp" 
             Black "images/bhorse.bmp")
      (moves
         (step-2 n nw) (step-2 n ne) (step-2 s sw) (step-2 s se)
         (step-2 w nw) (step-2 e ne) (step-2 w sw) (step-2 e se)
      )
   )
   (piece
      (name Elephant)
      (image Red "images/relephant.bmp" 
             Black "images/belephant.bmp")
      (moves
         (step-e n home) (step-e e home) (step-e w home) (step-e s home)
      )
   )
   (piece
      (name Chariot)
      (image Red "images/rchariot.bmp" 
             Black "images/bchariot.bmp")
      (moves
         (slide n) (slide s) (slide w) (slide e)
      )
   )
   (piece
      (name Cannon)
      (image Red "images/rcannon.bmp" 
             Black "images/bcannon.bmp")
      (moves
         (shoot n) (shoot s) (shoot w) (shoot e)
      )
   )
   (piece
      (name Dun)
      (image Red "images/rdun.bmp" 
             Black "images/bdun.bmp")
      (moves
         (step-pawn ne) (step-pawn sw) (step-pawn nw) (step-pawn se) 
      )
   )
   (piece
      (name Mandarin)
      (image Red "images/rmandarin.bmp" 
             Black "images/bmandarin.bmp")
      (moves
         (step n fortress) (step e fortress) (step w fortress) (step s fortress)
      )
   )
   (piece
      (name General)
      (image Red "images/rgeneral.bmp"  
             Black "images/bgeneral.bmp")
      (moves
         (step n fortress) (step s fortress) (step w fortress) (step e fortress)
         (step ne cross) (step sw cross) (step nw cross) (step se cross)
      )
   )

   (board-setup
      (Red
         (Soldier c1 e3 g5 i7 k9)
         (Horse h1 k4)
         (Elephant i1 k3)
         (Chariot e1 k7)
         (Cannon g1 k5)
         (Dun g3 i5)
         (Mandarin j1 k2)
         (General k1)
      )
      (Black
         (Soldier a3 c5 e7 g9 i11)
         (Horse d11 a8)
         (Elephant c11 a9)
         (Chariot g11 a5)
         (Cannon e11 a7)
         (Dun c7 e9)
         (Mandarin b11 a10)
         (General a11)
      )
   )
)
