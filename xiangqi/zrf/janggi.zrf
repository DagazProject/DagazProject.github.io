(define drop (
  (verify (in-zone? setup-place))
  (verify empty?)
  add
))

(define step-palace (
  $1 (verify not-friend?)
  (verify (in-zone? palace))
  add
))

(define step-fortress (
  (verify (in-zone? fortress))
  $1 (verify not-friend?)
  (verify (in-zone? fortress))
  add
))

(define step-pawn (
  $1 (verify not-friend?)
  add
))

(define step-horse (
  $1 (verify empty?)
  $2 (verify not-friend?)
  add
))

(define step-elephant (
  $1 (verify empty?)
  $2 (verify empty?)
  $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty?
     add
     $1
  )
  (verify not-friend?)
  add
))

(define slide-fortress (
  (verify (in-zone? fortress))
  $1 
  (while empty?
     (verify (in-zone? fortress))
     add
     $1
  )
  (verify (in-zone? fortress))
  (verify not-friend?)
  add
))

(define shoot (
  $1 (while empty? $1)
  (verify (not-piece? Cannon)) $1
  (while empty? add $1)
  (verify not-friend?)
  (verify (not-piece? Cannon))
  add
))

(define shoot-fortress (
  (verify (in-zone? fortress))
  $1 (verify not-empty?) 
  (verify (not-piece? Cannon)) 
  $1 (verify (in-zone? fortress))
  (verify not-friend?)
  (verify (not-piece? Cannon))
  add
))

(game
   (title "Janggi")

   (option "smart moves"       true)
   (option "animate captures"  false)

   (move-sound "Audio\Pickup.wav")
   (release-sound "Audio\Pickup.wav")
   (capture-sound "")

   (players Red Black)
   (turn-order 
       Red Red Red Red
       Black Black Black Black
       repeat Red Black
   )
   (move-priorities drop-type normal-type)

   (board
      (image "images/janggi.bmp")
      (grid
         (start-rectangle 4 5 46 47)
         (dimensions
             ("a/b/c/d/e/f/g/h/i" (45 0)) ; files
             ("10/9/8/7/6/5/4/3/2/1" (0 45)) ; ranks
         )
         (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                     (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
         )
      )
      (symmetry Black (n s) (s n) (w e) (e w) (nw se) (se nw) (ne sw) (sw ne))
      (zone
         (name palace)
         (players Red)
         (positions d1 e1 f1 d2 e2 f2 d3 e3 f3)
      )
      (zone
         (name palace)
         (players Black)
         (positions d10 e10 f10 d9 e9 f9 d8 e8 f8)
      )
      (zone
         (name fortress)
         (players Red Black)
         (positions d1 d3 e2 f1 f3 d8 d10 e9 f8 f10)
      )
      (zone
         (name setup-place)
         (players Red)
         (positions b1 c1 g1 h1)
      )
      (zone
         (name setup-place)
         (players Black)
         (positions b10 c10 g10 h10)
      )
   )

   (piece
      (name Soldier)
      (help "Soldier: 1 step forward, left, or right")
      (image Red "images/rsoldier.bmp" 
             Black "images/bsoldier.bmp")
      (moves
         (move-type normal-type)
         (step-pawn n) (step-pawn w) (step-pawn e)
         (step-fortress nw) (step-fortress ne)
      )
   )
   (piece
      (name Horse)
      (help "Horse/Knight: step orthogonally 1 square then diagonally outward 1 square")
      (image Red "images/rhorse.bmp" 
             Black "images/bhorse.bmp")
      (drops
         (move-type drop-type)
         (drop)
      )
      (moves
         (move-type normal-type)
         (step-horse n nw) (step-horse n ne) (step-horse s sw) (step-horse s se)
         (step-horse w nw) (step-horse e ne) (step-horse w sw) (step-horse e se)
      )
   )
   (piece
      (name Elephant)
      (help "Elephant: moves 1 step orthogonally plus 2 diagonally outward")
      (image Red "images/relephant.bmp" 
             Black "images/belephant.bmp")
      (drops
         (move-type drop-type)
         (drop)
      )
      (moves
         (move-type normal-type)
         (step-elephant n nw) (step-elephant n ne) (step-elephant s sw) (step-elephant s se)
         (step-elephant w nw) (step-elephant e ne) (step-elephant w sw) (step-elephant e se)
      )
   )
   (piece
      (name Chariot)
      (help "Chariot/Rook: slides any number of squares along a row or column")
      (image Red "images/rchariot.bmp" 
             Black "images/bchariot.bmp")
      (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
         (slide-fortress nw) (slide-fortress ne)
         (slide-fortress sw) (slide-fortress se)
      )
   )
   (piece
      (name Cannon)
      (help "Cannon: slides on row or column, captures by jumping")
      (image Red "images/rcannon.bmp" 
             Black "images/bcannon.bmp")
      (moves
         (move-type normal-type)
         (shoot n) (shoot s) (shoot w) (shoot e)
         (shoot-fortress nw) (shoot-fortress ne)
         (shoot-fortress sw) (shoot-fortress se)
      )
   )
   (piece
      (name Mandarin)
      (help "Mandarin: 1 step along lines in fortress")
      (image Red "images/rmandarin.bmp" 
             Black "images/bmandarin.bmp")
      (moves
         (move-type normal-type)
         (step-palace n)  (step-palace s) (step-fortress nw) (step-fortress sw)
         (step-palace w)  (step-palace e) (step-fortress ne) (step-fortress se)
      )
   )
   (piece
      (name General)
      (help "General: 1 step along lines in fortress, can't `see` other General")
      (image Red "images/rgeneral.bmp"  
             Black "images/bgeneral.bmp")
      (moves
         (move-type normal-type)
         (step-palace n)  (step-palace s) (step-fortress nw) (step-fortress sw)
         (step-palace w)  (step-palace e) (step-fortress ne) (step-fortress se)
      )
   )

   (board-setup
      (Red
         (Soldier a4 c4 e4 g4 i4)
         (Horse off 2)
         (Elephant off 2)
         (Chariot a1 i1)
         (Mandarin d1 f1)
         (Cannon b3 h3)
         (General e2)
      )
      (Black
         (Soldier a7 c7 e7 g7 i7)
         (Horse off 2)
         (Elephant off 2)
         (Chariot a10 i10)
         (Mandarin d10 f10)
         (Cannon b8 h8)
         (General e9)
      )
   )

   (loss-condition (Red Black) (checkmated General))
   (loss-condition (Red Black) stalemated)
   (loss-condition (Red Black) repetition)
)

(variant
   (title "Koma Janggi")
   (default)

   (turn-order Red Black)

   (board
      (image "images/7x5.bmp")
      (grid
         (start-rectangle 4 5 46 47)
         (dimensions
             ("a/b/c/d/e" (45 0)) ; files
             ("7/6/5/4/3/2/1" (0 45)) ; ranks
         )
         (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                     (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
         )
      )
      (symmetry Black (n s) (s n) (w e) (e w) (nw se) (se nw) (ne sw) (sw ne))
      (zone
         (name palace)
         (players Red)
         (positions b1 c1 d1 b2 c2 d2 b3 c3 d3)
      )
      (zone
         (name palace)
         (players Black)
         (positions b7 c7 d7 b6 c6 d6 b5 c5 d5)
      )
      (zone
         (name fortress)
         (players Red Black)
         (positions b1 b3 c2 d1 d3 b7 b5 c6 d7 d5)
      )
   )

   (board-setup
      (Red
         (Soldier a3 c3 e3)
         (Horse b1 d1)
         (Chariot a1 e1)
         (General c2)
      )
      (Black
         (Soldier a5 c5 e5)
         (Horse b7 d7)
         (Chariot a7 e7)
         (General c6)
      )
   )
)
