(define step (
  $1 (verify not-friend?)
  (verify (in-zone? $2))
  add
))

(define step-pawn (
  $1 (verify not-friend?)
  add
))

(define step-2 (
  $1 (verify empty?)
  $2 (verify not-friend?)
  add
))

(define step-e (
  $1 (verify empty?)
  $1 (verify not-friend?)
  (verify (in-zone? $2))
  add
))

(define slide (
  $1 
  (while empty?
     add
     $1
  )
  (verify not-friend?)
  add
))

(define shoot (
  $1 
  (while empty?
     add
     $1
  )
  (verify not-empty?) $1
  (while empty? $1)
  (verify not-friend?)
  add
))

(define shootm (
  $1 (while empty? $1)
  (verify not-empty?) $1
  (while empty? $1)
  (verify not-friend?)
  add
))

(game
   (title "XiangQi")

   (option "smart moves"       true)
   (option "animate captures"  false)
   (option "ko"                situation)
   (option "xiangqi-extension" true)

   (move-sound "Audio\Pickup.wav")
   (release-sound "Audio\Pickup.wav")
   (capture-sound "")

   (players Red Black)
   (turn-order Red Black)

   (board
      (image "images/10x9.bmp")
      (grid
         (start-rectangle 4 5 46 47)
         (dimensions
             ("a/b/c/d/e/f/g/h/i" (45 0)) ; files
             ("10/9/8/7/6/5/4/3/2/1" (0 45)) ; ranks
         )
         (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                     (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
         )
      )
      (symmetry Black (n s) (s n) (w e) (e w) (nw se) (se nw) (ne sw) (sw ne))
      (zone
         (name fortress)
         (players Red)
         (positions d1 e1 f1 d2 e2 f2 d3 e3 f3)
      )
      (zone
         (name fortress)
         (players Black)
         (positions d10 e10 f10 d9 e9 f9 d8 e8 f8)
      )
      (zone
         (name home)
         (players Red)
         (positions a1 b1 c1 d1 e1 f1 g1 h1 i1
                    a2 b2 c2 d2 e2 f2 g2 h2 i2
                    a3 b3 c3 d3 e3 f3 g3 h3 i3
                    a4 b4 c4 d4 e4 f4 g4 h4 i4
                    a5 b5 c5 d5 e5 f5 g5 h5 i5)
      )
      (zone
         (name home)
         (players Black)
         (positions a10 b10 c10 d10 e10 f10 g10 h10 i10
                    a9 b9 c9 d9 e9 f9 g9 h9 i9
                    a8 b8 c8 d8 e8 f8 g8 h8 i8
                    a7 b7 c7 d7 e7 f7 g7 h7 i7
                    a6 b6 c6 d6 e6 f6 g6 h6 i6)
      )
      (zone
         (name enemy)
         (players Black)
         (positions a1 b1 c1 d1 e1 f1 g1 h1 i1
                    a2 b2 c2 d2 e2 f2 g2 h2 i2
                    a3 b3 c3 d3 e3 f3 g3 h3 i3
                    a4 b4 c4 d4 e4 f4 g4 h4 i4
                    a5 b5 c5 d5 e5 f5 g5 h5 i5)
      )
      (zone
         (name enemy)
         (players Red)
         (positions a10 b10 c10 d10 e10 f10 g10 h10 i10
                    a9 b9 c9 d9 e9 f9 g9 h9 i9
                    a8 b8 c8 d8 e8 f8 g8 h8 i8
                    a7 b7 c7 d7 e7 f7 g7 h7 i7
                    a6 b6 c6 d6 e6 f6 g6 h6 i6)
      )
   )

   (piece
      (name Soldier)
	  (help "Soldier/Pawn: only moves forward until it crosses the river, then also left and right")
      (image Red "images/rsoldier.bmp" 
             Black "images/bsoldier.bmp")
      (moves
         (step-pawn n) (step w enemy) (step e enemy)
      )
   )
   (piece
      (name Horse)
      (help "Horse/Knight: step orthogonally 1 square then diagonally outward 1 square")
      (image Red "images/rhorse.bmp" 
             Black "images/bhorse.bmp")
      (moves
         (step-2 n nw) (step-2 n ne) (step-2 s sw) (step-2 s se)
         (step-2 w nw) (step-2 e ne) (step-2 w sw) (step-2 e se)
      )
   )
   (piece
      (name Elephant)
      (help "Elephant: moves two steps diagonally without jumping, can't cross the river")
      (image Red "images/relephant.bmp" 
             Black "images/belephant.bmp")
      (moves
         (step-e nw home) (step-e sw home) (step-e ne home) (step-e se home)
      )
   )
   (piece
      (name Chariot)
      (help "Chariot/Rook: slides any number of squares along a row or column")
      (image Red "images/rchariot.bmp" 
             Black "images/bchariot.bmp")
      (moves
         (slide n) (slide s) (slide w) (slide e)
      )
   )
   (piece
      (name Cannon)
      (help "Cannon: slides on row or column, captures by jumping")
      (image Red "images/rcannon.bmp" 
             Black "images/bcannon.bmp")
      (moves
         (shoot n) (shoot s) (shoot w) (shoot e)
      )
   )
   (piece
      (name Mandarin)
      (help "Mandarin: steps along diagonal lines in fortress")
      (image Red "images/rmandarin.bmp" 
             Black "images/bmandarin.bmp")
      (moves
         (step nw fortress) (step sw fortress) (step ne fortress) (step se fortress)
      )
   )
   (piece
      (name General)
      (help "General/King: steps orthogonally within fortress, can't oppose opponent's General")
      (image Red "images/rgeneral.bmp"  
             Black "images/bgeneral.bmp")
      (moves
         (step n fortress) (step s fortress) (step w fortress) (step e fortress)
      )
   )

   (board-setup
      (Red
         (Soldier a4 c4 e4 g4 i4)
         (Horse b1 h1)
         (Elephant c1 g1)
         (Chariot a1 i1)
         (Mandarin d1 f1)
         (Cannon b3 h3)
         (General e1)
      )
      (Black
         (Soldier a7 c7 e7 g7 i7)
         (Horse b10 h10)
         (Elephant c10 g10)
         (Chariot a10 i10)
         (Mandarin d10 f10)
         (Cannon b8 h8)
         (General e10)
      )
   )

   (loss-condition (Red Black) (checkmated General))
   (loss-condition (Red Black) stalemated)
   (loss-condition (Red Black) repetition)
)

(variant
   (title "Mini XiangQi")
   (history "Invented by Robert Price.")

   (board
      (image "images/6x7.bmp")
      (grid
         (start-rectangle 4 5 46 47)
         (dimensions
             ("a/b/c/d/e/f/g" (45 0)) ; files
             ("6/5/4/3/2/1" (0 45)) ; ranks
         )
         (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                     (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
         )
      )
      (symmetry Black (n s)(s n) (nw sw)(sw nw) (ne se)(se ne))
      (zone
         (name fortress)
         (players Red)
         (positions c1 d1 e1 c2 d2 e2 c3 d3 e3)
      )
      (zone
         (name fortress)
         (players Black)
         (positions c4 d4 e4 c5 d5 e5 c6 d6 e6)
      )
      (zone
         (name home)
         (players Red)
         (positions a1 b1 c1 d1 e1 f1 g1
                    a2 b2 c2 d2 e2 f2 g2
                    a3 b3 c4 d3 e3 f3 g3)
      )
      (zone
         (name home)
         (players Black)
         (positions a4 b4 c4 d4 e4 f4 g4
                    a5 b5 c5 d5 e5 f5 g5
                    a6 b6 c6 d6 e6 f6 g6)
      )
      (zone
         (name enemy)
         (players Black)
         (positions a1 b1 c1 d1 e1 f1 g1
                    a2 b2 c2 d2 e2 f2 g2
                    a3 b3 c4 d3 e3 f3 g3)
      )
      (zone
         (name enemy)
         (players Red)
         (positions a4 b4 c4 d4 e4 f4 g4
                    a5 b5 c5 d5 e5 f5 g5
                    a6 b6 c6 d6 e6 f6 g6)
      )
   )

   (board-setup
      (Red
         (Soldier a3 d3 g3)
         (Horse b1 f1)
         (Chariot a1 g1)
         (Mandarin c1 e1)
         (Cannon b2 f2)
         (General d1)
      )
      (Black
         (Soldier a4 d4 g4)
         (Horse b6 f6)
         (Chariot a6 g6)
         (Mandarin c6 e6)
         (Cannon b5 f5)
         (General d6)
      )
   )
)

(variant
   (title "Yitongqi (Black)")

   (piece
      (name Manchu)
      (image Red "images/rchariot.bmp" 
             Black "images/bchariot.bmp")
      (moves
         (slide n) (slide s) (slide w) (slide e)
         (step-2 n nw) (step-2 n ne) (step-2 s sw) (step-2 s se)
         (step-2 w nw) (step-2 e ne) (step-2 w sw) (step-2 e se)
         (shootm n) (shootm s) (shootm w) (shootm e)
      )
   )

   (board-setup
      (Red
         (Soldier a4 c4 e4 g4 i4)
         (Horse b1 h1)
         (Elephant c1 g1)
         (Chariot a1 i1)
         (Mandarin d1 f1)
         (Cannon b3 h3)
         (General e1)
      )
      (Black
         (Soldier a7 c7 e7 g7 i7)
         (Elephant c10 g10)
         (Manchu i10)
         (Mandarin d10 f10)
         (General e10)
      )
   )
)

(variant
   (title "5 Tigers (Black)")
   (default)

   (turn-order Red Black Black)

   (piece
      (name Tiger)
      (image Red "images/rsoldier.bmp" 
             Black "images/bsoldier.bmp")
      (moves
         (step-pawn n) (step w enemy) (step e enemy)
      )
   )

   (board-setup
      (Red
         (Soldier a4 c4 e4 g4 i4)
         (Horse b1 h1)
         (Elephant c1 g1)
         (Chariot a1 i1)
         (Mandarin d1 f1)
         (Cannon b3 h3)
         (General e1)
      )
      (Black
         (Tiger a7 c7 e7 g7 i7)
         (Elephant c10 g10)
         (Mandarin d10 f10)
         (General e10)
      )
   )
)
