(define shift (
  $1 (verify empty?)
  add
))

(define jump (
  $1 $1 (verify enemy?)
  add
))

(define step (
  $1 (verify not-friend?)
  (verify (in-zone? $2))
  add
))

(define step-pawn (
  $1 (verify not-friend?)
  add
))

(define step-2 (
  $1 (verify empty?)
  $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty?
     add
     $1
  )
  (verify not-friend?)
  add
))

(define shoot (
  $1 
  (while empty?
     add
     $1
  )
  (verify not-empty?) $1
  (while empty? $1)
  (verify not-friend?)
  add
))

(game
   (title "MiniXiangQi")

   (players Red Black)
   (turn-order Red Black)

   (board
      (image "images/7x7.bmp")
      (grid
         (start-rectangle 4 5 46 47)
         (dimensions
             ("a/b/c/d/e/f/g" (45 0)) ; files
             ("7/6/5/4/3/2/1" (0 45)) ; ranks
         )
         (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                     (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
         )
      )
      (symmetry Black (n s) (s n) (w e) (e w) (nw se) (se nw) (ne sw) (sw ne))
      (zone
         (name fortress)
         (players Red)
         (positions c1 d1 e1 c2 d2 e2 c3 d3 e3)
      )
      (zone
         (name fortress)
         (players Black)
         (positions c7 d7 e7 c6 d6 e6 c5 d5 e5)
      )
      (zone
         (name promotion)
         (players Red)
         (positions a7 b7 c7 d7 e7 f7 g7 a6 b6 c6 d6 e6 f6 g6)
      )
      (zone
         (name promotion)
         (players Black)
         (positions a1 b1 c1 d1 e1 f1 g1 a2 b2 c2 d2 e2 f2 g2)
      )
   )

   (piece
      (name Soldier)
      (image Red "images/kanji/wsoldier.bmp" 
             Black "images/kanji/bsoldier.bmp")
      (moves
         (step-pawn n) (step-pawn w) (step-pawn e)
      )
   )
   (piece
      (name Horse)
      (image Red "images/kanji/whorse.bmp" 
             Black "images/kanji/bhorse.bmp")
      (moves
         (step-2 n nw) (step-2 n ne) (step-2 s sw) (step-2 s se)
         (step-2 w nw) (step-2 e ne) (step-2 w sw) (step-2 e se)
      )
   )
   (piece
      (name Chariot)
      (image Red "images/kanji/wchariot.bmp" 
             Black "images/kanji/bchariot.bmp")
      (moves
         (slide n) (slide s) (slide w) (slide e)
      )
   )
   (piece
      (name Cannon)
      (image Red "images/kanji/wcannon.bmp" 
             Black "images/kanji/bcannon.bmp")
      (moves
         (shoot n) (shoot s) (shoot w) (shoot e)
      )
   )
   (piece
      (name Leopard)
      (image Red "images/kanji/wleopard.bmp" 
             Black "images/kanji/bleopard.bmp")
      (moves
         (shift n) (shift s) (shift w) (shift e)
         (jump ne) (jump sw) (jump nw) (jump se)
      )
   )
   (piece
      (name General)
      (image Red "images/kanji/wgeneral.bmp"  
             Black "images/kanji/bgeneral.bmp")
      (moves
         (step n fortress) (step s fortress) (step w fortress) (step e fortress)
      )
   )

   (board-setup
      (Red
         (Soldier a2 c2 d2 e2 g2)
         (Horse c1 e1)
         (Chariot a1 g1)
         (Cannon b2 f2)
         (Leopard b1 f1)
         (General d1)
      )
      (Black
         (Soldier a6 c6 d6 e6 g6)
         (Horse c7 e7)
         (Chariot a7 g7)
         (Cannon b6 f6)
         (Leopard b7 f7)
         (General d7)
      )
   )
)
