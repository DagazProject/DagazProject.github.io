(define drop-stone (
  (verify empty?)
  add
))

(define general-step (
  $1 (verify friend?)
  (verify (piece? Stone))
  add
))

(define mandarin-step (
  $1 (verify not-empty?)
  (verify (piece? Stone))
  add
))

(define step (
  $1 add
))

(define step-2 (
  $1 (verify (or empty? (piece? Stone)))
  $2 add
))

(define slide (
  $1 (while (or empty? (piece? Stone)) add $1)
  add
))

(define shoot (
  $1 (while (or empty? (piece? Stone)) add $1)
  $1 (while (or empty? (piece? Stone)) $1)
  add
))

(game
   (title "Chess Go")

   (players Black White)
   (turn-order Black White)

   (board
      (image "images/chessgo/9x9-mobile.bmp")
      (grid
         (start-rectangle 59 5 101 47)
         (dimensions
             ("a/b/c/d/e/f/g/h/i" (45 0)) ; files
             ("9/8/7/6/5/4/3/2/1" (0 45)) ; ranks
         )
         (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                     (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
         )
      )
      (grid
         (start-rectangle 5 2 50 50)
         (dimensions
             ("X/Y" (466 0)) ; files
             ("9/8/7/6/5/4/3/2/1" (0 45)) ; ranks
         )
      )
   )

   (piece
      (name Stone)
      (image Black "images/chessgo/bstone.bmp"
             White "images/chessgo/wstone.bmp")
      (drops
         (drop-stone)
      )
   )
   (piece
      (name Soldier)
      (image Black "images/chessgo/bsoldier.bmp"
             White "images/chessgo/wsoldier.bmp")
      (moves
         (step n) (step e) (step w) (step s)
      )
   )
   (piece
      (name Horse)
      (image Black "images/chessgo/bhorse.bmp"
             White "images/chessgo/whorse.bmp")
      (moves
         (step-2 n nw) (step-2 n ne) (step-2 s sw) (step-2 s se)
         (step-2 w nw) (step-2 e ne) (step-2 w sw) (step-2 e se)
      )
   )
   (piece
      (name Elephant)
      (image Black "images/chessgo/belephant.bmp"
             White "images/chessgo/welephant.bmp")
      (moves
         (step-2 nw nw) (step-2 ne ne)
         (step-2 sw sw) (step-2 se se)
      )
   )
   (piece
      (name Chariot)
      (image Black "images/chessgo/bchariot.bmp"
             White "images/chessgo/wchariot.bmp")
      (moves
         (slide n) (slide e) (slide w) (slide s)
      )
   )
   (piece
      (name Cannon)
      (image Black "images/chessgo/bcannon.bmp"
             White "images/chessgo/wcannon.bmp")
      (moves
         (shoot n) (shoot e) (shoot w) (shoot s)
      )
   )
   (piece
      (name Mandarin)
      (image Black "images/chessgo/bmandarin.bmp"
             White "images/chessgo/wmandarin.bmp")
      (moves
         (mandarin-step nw) (mandarin-step ne) 
         (mandarin-step sw) (mandarin-step se)
      )
   )
   (piece
      (name General)
      (image Black "images/chessgo/bgeneral.bmp"
             White "images/chessgo/wgeneral.bmp")
      (moves
         (general-step n) (general-step e) (general-step w) (general-step s)
      )
   )

   (board-setup
      (White
         (Soldier X6 X5)
         (Horse X4)
         (Elephant X3)
         (Chariot X2)
         (Mandarin X8 X7)
         (Cannon X1)
         (General X9)
      )
      (Black
         (Soldier Y6 Y5)
         (Horse Y4)
         (Elephant Y3)
         (Chariot Y2)
         (Mandarin Y8 Y7)
         (Cannon Y1)
         (General Y9)
      )
   )
)
