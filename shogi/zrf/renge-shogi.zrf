(define drop-piece (
  (verify (not-in-zone? board-zone)) a1
  (while (on-board? $1)
     (if empty? add) $1
  )
  (verify empty?) add
))

(define step (
  (verify (in-zone? board-zone))
  (verify friend?)
  $1 (verify not-friend?)
  add
))

(define leap (
  (verify (in-zone? board-zone))
  (verify friend?)
  $1 (verify not-friend?)
  (add $2)
))

(define jump (
  (verify (in-zone? board-zone))
  (verify friend?)
  $1 $2 (verify not-friend?)
  (add $3)
))

(define slide (
  (verify (in-zone? board-zone)) 
  (verify friend?) $1
  (while empty?
     (add $2)
     $1
  )
  (verify enemy?)
  (add $2)
))

(game
  (title "Renge Shogi")
  (history "Copyright (c) 1999 Michel Sandeman.")

  (option "animate captures"      false)
  (option "smart moves"           false)

  (players South North)
  (turn-order South North)

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (board
        (image "images/7x7.bmp")
        (grid
             (start-rectangle 9 15 50 61)
             (dimensions ("X/Y/I/a/b/c/d/e/f/g/J/Z/T" (41 0))
                         ("7/6/5/4/3/2/1" (0 46))
             )
	     (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                         (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
                         (nx 0 -1)
	     )
        )
        (links sr
               (a1 Z1) (a2 Z1) (a3 Z1) (a4 Z1) (a5 Z1) (a6 Z1) (a7 Z1)
               (b1 Z1) (b2 Z1) (b3 Z1) (b4 Z1) (b5 Z1) (b6 Z1) (b7 Z1)
               (c1 Z1) (c2 Z1) (c3 Z1) (c4 Z1) (c5 Z1) (c6 Z1) (c7 Z1)
               (d1 Z1) (d2 Z1) (d3 Z1) (d4 Z1) (d5 Z1) (d6 Z1) (d7 Z1)
               (e1 Z1) (e2 Z1) (e3 Z1) (e4 Z1) (e5 Z1) (e6 Z1) (e7 Z1)
               (f1 Z1) (f2 Z1) (f3 Z1) (f4 Z1) (f5 Z1) (f6 Z1) (f7 Z1)
               (g1 Z1) (g2 Z1) (g3 Z1) (g4 Z1) (g5 Z1) (g6 Z1) (g7 Z1)
               (Z1 Z2) (Z2 Z3) (Z3 Z4) (Z4 Z5) (Z5 Z6) (Z6 Z7)
        )
        (links nr
               (a1 Y7) (a2 Y7) (a3 Y7) (a4 Y7) (a5 Y7) (a6 Y7) (a7 Y7)
               (b1 Y7) (b2 Y7) (b3 Y7) (b4 Y7) (b5 Y7) (b6 Y7) (b7 Y7)
               (c1 Y7) (c2 Y7) (c3 Y7) (c4 Y7) (c5 Y7) (c6 Y7) (c7 Y7)
               (d1 Y7) (d2 Y7) (d3 Y7) (d4 Y7) (d5 Y7) (d6 Y7) (d7 Y7)
               (e1 Y7) (e2 Y7) (e3 Y7) (e4 Y7) (e5 Y7) (e6 Y7) (e7 Y7)
               (f1 Y7) (f2 Y7) (f3 Y7) (f4 Y7) (f5 Y7) (f6 Y7) (f7 Y7)
               (g1 Y7) (g2 Y7) (g3 Y7) (g4 Y7) (g5 Y7) (g6 Y7) (g7 Y7)
               (Y7 Y6) (Y6 Y5) (Y5 Y4) (Y4 Y3) (Y3 Y2) (Y2 Y1)
        )
        (links nx 
               (a7 b1) (b7 c1) (c7 d1) (d7 e1) (e7 f1) (f7 g1)
        )
        (links pr
               (Z1 T1) (T1 Z1) (Z2 T2) (T2 Z2) (Z3 T3) (T3 Z3) (Z4 T4) 
               (T4 Z4) (Z5 T5) (T5 Z5) (Z6 T6) (T6 Z6) (Z7 T7) (T7 Z7)
               (X1 Y1) (Y1 X1) (X2 Y2) (Y2 X2) (X3 Y3) (Y3 X3) (X4 Y4) 
               (Y4 X4) (X5 Y5) (Y5 X5) (X6 Y6) (Y6 X6) (X7 Y7) (Y7 X7)
        )
        (symmetry North (n s) (s n) (w e) (e w) (nw se) (se nw) (ne sw) (sw ne) (sr nr) (nr sr) (nx nx) (pr pr))
        (kill-positions I1 I2 I3 I4 I5 I6 I7 J1 J2 J3 J4 J5 J6 J7)
        (zone (name board-zone) (players South North)
              (positions a1 b1 c1 d1 e1 f1 g1
                         a2 b2 c2 d2 e2 f2 g2
                         a3 b3 c3 d3 e3 f3 g3
                         a4 b4 c4 d4 e4 f4 g4
                         a5 b5 c5 d5 e5 f5 g5
                         a6 b6 c6 d6 e6 f6 g6
                         a7 b7 c7 d7 e7 f7 g7
              )
        )
  )

  (piece 
       (name King)
       (image South "images/sking.bmp" 
              North "images/nking.bmp")
       (moves
           (step n) (step nw)
           (step s) (step se)
           (step w) (step sw)
           (step e) (step ne)
       )
  )
  (piece 
       (name Gold)
       (image South "images/sgold.bmp" 
              North "images/ngold.bmp")
       (moves
           (leap n Knight) (leap nw Knight)
           (leap s Knight) (leap ne Knight)
           (leap w Knight) (leap e  Knight) 
           (drop-piece nx)
       )
  )
  (piece 
       (name Knight)
       (image South "images/sknight.bmp" 
              North "images/nknight.bmp")
       (moves
           (jump n nw Gold)
           (jump n ne Gold)
           (drop-piece nx)
       )
  )
  (piece 
       (name Silver)
       (image South "images/ssilver.bmp" 
              North "images/nsilver.bmp")
       (moves
           (leap nw Lance) (leap sw Lance)
           (leap se Lance) (leap ne Lance)
           (leap n  Lance)
           (drop-piece nx)
       )
  )
  (piece 
       (name Lance)
       (image South "images/slance.bmp" 
              North "images/nlance.bmp")
       (moves
           (slide n Silver)
           (drop-piece nx)
       )
  )
  (piece 
       (name LeftPawn)
       (image South "images/spawn.bmp" 
              North "images/npawn.bmp")
       (moves
           (leap n Bishop)
           (drop-piece nx)
       )
  )
  (piece 
       (name Bishop)
       (image South "images/sbishop.bmp" 
              North "images/nbishop.bmp")
       (moves
           (slide nw LeftPawn)
           (slide se LeftPawn)
           (slide sw LeftPawn)
           (slide ne LeftPawn)
           (drop-piece nx)
       )
  )
  (piece 
       (name RightPawn)
       (image South "images/spawn.bmp" 
              North "images/npawn.bmp")
       (moves
           (leap n Rook)
           (drop-piece nx)
       )
  )
  (piece 
       (name Rook)
       (image South "images/srook.bmp" 
              North "images/nrook.bmp")
       (moves
           (slide n RightPawn)
           (slide e RightPawn)
           (slide w RightPawn)
           (slide s RightPawn)
           (drop-piece nx)
       )
  )

  (board-setup
        ( South 
            (King      d1)
            (Gold      c1 e1)
            (Silver    a1 g1)
            (LeftPawn  b1)
            (RightPawn f1)
        )
        ( North
            (King      d7)
            (Gold      c7 e7)
            (Silver    a7 g7)
            (LeftPawn  f7)
            (RightPawn b7)
        )
  )

  (loss-condition (South North) (pieces-remaining 0 King))
  (loss-condition (South North) repetition)
)
