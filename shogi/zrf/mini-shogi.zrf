(define drop-piece (
  (verify (not-in-zone? board-zone)) a1
  (while (on-board? $1)
     (if empty? add) $1
  )
))

(define step (
  (verify (in-zone? board-zone))
  $1 (verify not-friend?)
  add
))

(define slide (
  (verify (in-zone? board-zone)) $1
  (while empty?
     add
     $1
  )
  (verify enemy?)
  add
))

(game
  (title "Mini Shogi")
  (history "Rediscovered by Shigeo Kusumoto around 1970.")

  (option "animate captures"      false)
  (option "smart moves"           false)
; (option "shogi promotion"       false)

  (players South North)
  (turn-order South North)

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (board
        (image "images/5x5.bmp")
        (grid
             (start-rectangle 9 15 50 61)
             (dimensions ("X/Y/I/a/b/c/d/e/J/Z/T" (41 0))
                         ("5/4/3/2/1" (0 46))
             )
	     (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                         (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
	     )
        )
        (links sr
               (a1 Z1) (a2 Z1) (a3 Z1) (a4 Z1) (a5 Z1)
               (b1 Z1) (b2 Z1) (b3 Z1) (b4 Z1) (b5 Z1)
               (c1 Z1) (c2 Z1) (c3 Z1) (c4 Z1) (c5 Z1)
               (d1 Z1) (d2 Z1) (d3 Z1) (d4 Z1) (d5 Z1)
               (e1 Z1) (e2 Z1) (e3 Z1) (e4 Z1) (e5 Z1)
               (Z1 T1) (T1 Z2) (Z2 T2) (T2 Z3) (Z3 T3) 
               (T3 Z4) (Z4 T4) (T4 Z5) (Z5 T5) (T5 X1)
               (X1 Y1) (Y1 X2) (X2 Y2) (Y2 X3) (X3 Y3) 
               (Y3 X4) (X4 Y4) (Y4 X5) (X5 Y5)
        )
        (links nr
               (a1 Y5) (a2 Y5) (a3 Y5) (a4 Y5) (a5 Y5)
               (b1 Y5) (b2 Y5) (b3 Y5) (b4 Y5) (b5 Y5)
               (c1 Y5) (c2 Y5) (c3 Y5) (c4 Y5) (c5 Y5)
               (d1 Y5) (d2 Y5) (d3 Y5) (d4 Y5) (d5 Y5)
               (e1 Y5) (e2 Y5) (e3 Y5) (e4 Y5) (e5 Y5)
               (Y5 X5) (X5 Y4) (Y4 X4) (X4 Y3) (Y3 X3) 
               (X3 Y2) (Y2 X2) (X2 Y1) (Y1 X1) (X1 T5) 
               (T5 Z5) (Z5 T4) (T4 Z4) (Z4 T3) (T3 Z3) 
               (Z3 T2) (T2 Z2) (Z2 T1) (T1 Z1)
        )
        (links nx 
               (a1 a2) (a2 a3) (a3 a4) (a4 a5) (a5 b1)
               (b1 b2) (b2 b3) (b3 b4) (b4 b5) (b5 c1)
               (c1 c2) (c2 c3) (c3 c4) (c4 c5) (c5 d1)
               (d1 d2) (d2 d3) (d3 d4) (d4 d5) (d5 e1)
               (e1 e2) (e2 e3) (e3 e4) (e4 e5) (e5 Z1)
        )
        (symmetry North (n s) (s n) (w e) (e w) (nw se) (se nw) (ne sw) (sw ne) (sr nr) (nr sr) (nx nx))
        (kill-positions I1 I2 I3 I4 I5 J1 J2 J3 J4 J5)
        (zone (name board-zone) (players South North)
              (positions a1 b1 c1 d1 e1
                         a2 b2 c2 d2 e2
                         a3 b3 c3 d3 e3
                         a4 b4 c4 d4 e4
                         a5 b5 c5 d5 e5
              )
        )
        (zone (name promotion-zone) (players South)
              (positions a5 b5 c5 d5 e5)
        )
        (zone (name promotion-zone) (players North)
              (positions a1 b1 c1 d1 e1)
        )
  )
  (piece 
       (name King)
       (image South "images/sking.bmp" 
              North "images/nking.bmp")
       (moves
           (step n) (step nw)
           (step s) (step se)
           (step w) (step sw)
           (step e) (step ne)
       )
  )
  (piece 
       (name Gold)
       (image South "images/sgold.bmp" 
              North "images/ngold.bmp")
       (moves
           (step n) (step nw)
           (step s) (step ne)
           (step w) (step e) 
           (drop-piece nx)
       )
  )
  (piece 
       (name Silver)
       (image South "images/ssilver.bmp" 
              North "images/nsilver.bmp")
       (moves
           (step nw) (step sw)
           (step se) (step ne)
           (step n)
           (drop-piece nx)
       )
  )
  (piece 
       (name SilverP)
       (image South "images/ssilverp.bmp" 
              North "images/nsilverp.bmp")
       (moves
           (step n) (step nw)
           (step s) (step ne)
           (step w) (step e) 
       )
  )
  (piece 
       (name Bishop)
       (image South "images/sbishop.bmp" 
              North "images/nbishop.bmp")
       (moves
           (slide nw)
           (slide se)
           (slide sw)
           (slide ne)
           (drop-piece nx)
       )
  )
  (piece 
       (name BishopP)
       (image South "images/sbishopp.bmp" 
              North "images/nbishopp.bmp")
       (moves
           (slide nw) (step n)
           (slide se) (step s)
           (slide sw) (step w)
           (slide ne) (step e)
       )
  )
  (piece 
       (name Rook)
       (image South "images/srook.bmp" 
              North "images/nrook.bmp")
       (moves
           (slide n)
           (slide e)
           (slide w)
           (slide s)
           (drop-piece nx)
       )
  )
  (piece 
       (name RookP)
       (image South "images/srookp.bmp" 
              North "images/nrookp.bmp")
       (moves
           (slide n) (step nw)
           (slide e) (step ne)
           (slide w) (step sw)
           (slide s) (step se)
       )
  )
  (piece 
       (name Pawn)
       (image South "images/spawn.bmp" 
              North "images/npawn.bmp")
       (moves
           (step n)
           (drop-piece nx)
       )
  )
  (piece 
       (name PawnP)
       (image South "images/spawnp.bmp" 
              North "images/npawnp.bmp")
       (moves
           (step n) (step nw)
           (step s) (step ne)
           (step w) (step e) 
       )
  )

  (board-setup
        ( South 
            (King   a1)
            (Gold   b1)
            (Silver c1)
            (Bishop d1)
            (Rook   e1)
            (Pawn   a2)
        )
        ( North
            (King   e5)
            (Gold   d5)
            (Silver c5)
            (Bishop b5)
            (Rook   a5)
            (Pawn   e4)
        )
  )

  (loss-condition (South North) (pieces-remaining 0 King))
  (loss-condition (South North) repetition)
)
