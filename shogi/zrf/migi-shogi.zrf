(define drop-piece (
  (verify (not-in-zone? board-zone)) a5
  (while (on-board? $1)
     (if empty? add) $1
  )
  (verify empty?)
  add
))

(define step (
  (verify (in-zone? board-zone))
  $1 (verify not-friend?)
  (if enemy?
      (add $2)
   else
      add
  )
))

(define jump (
  (verify (in-zone? board-zone))
  $1 $2 (verify not-friend?)
  (if enemy?
      (add $3)
   else
      add
  )
))

(define slide (
  (verify (in-zone? board-zone)) $1
  (while empty?
     add
     $1
  )
  (verify enemy?)
  (add $2)
))

(game
  (title "Migi Shogi")

  (players South North NS NN)
  (turn-order South North)

  (board
      (image "images/migi/board.bmp")
      (grid
           (start-rectangle 672 30 774 132)
           (dimensions ("a/b/c/d/e" (132 0))
                       ("5/4/3/2/1" (0 132))
           )
           (directions (n 0 -1) (e 1 0) (s 0 1) (w -1 0)
                       (ne 1 -1) (nw -1 -1) (se 1 1) (sw -1 1)
           )
      )
      (grid
        (start-rectangle 29 30 131 132)
           (dimensions ("A/B" (132 0))
                       ("5/4/3/2/1" (0 132))
        )
      )
      (grid
        (start-rectangle 317 30 419 132)
           (dimensions ("C/D" (132 0))
                       ("5/4/3/2/1" (0 132))
        )
      )
      (grid
        (start-rectangle 1375 30 1477 132)
           (dimensions ("E/F" (132 0))
                       ("5/4/3/2/1" (0 132))
        )
      )
      (grid
        (start-rectangle 1663 30 1765 132)
           (dimensions ("G/H" (132 0))
                       ("5/4/3/2/1" (0 132))
        )
      )
      (links nx
        (a5 a4) (a4 a3) (a3 a2) (a2 a1) (a1 b5)
        (b5 b4) (b4 b3) (b3 b2) (b2 b1) (b1 c5)
        (c5 c4) (c4 c3) (c3 c2) (c2 c1) (c1 d5)
        (d5 d4) (d4 d3) (d3 d2) (d2 d1) (d1 e5)
        (e5 e4) (e4 e3) (e3 e2) (e2 e1)
      )
      (symmetry North (n s) (s n) (w w) (e e) (nw se) (se nw) (ne sw) (sw ne) (nx nx))
      (zone (name board-zone) (players South North)
            (positions a1 b1 c1 d1 e1
                       a2 b2 c2 d2 e2
                       a3 b3 c3 d3 e3
                       a4 b4 c4 d4 e4
                       a5 b5 c5 d5 e5
            )
      )
      (zone (name last-rank) (players North)
            (positions a1 b1 c1 d1 e1)
      )
      (zone (name last-rank) (players South)
            (positions a5 b5 c5 d5 e5)
      )
      (zone (name last-ranks) (players North)
            (positions a1 b1 c1 d1 e1
                       a2 b2 c2 d2 e2
            )
      )
      (zone (name last-ranks) (players South)
            (positions a4 b4 c4 d4 e4
                       a5 b5 c5 d5 e5
            )
      )
  )

  (piece 
       (name King)
       (image South "images/migi/sking.bmp" 
              North "images/migi/nking.bmp")
       (moves
           (move-type normal-type)
           (step n King) (step nw King)
           (step s King) (step se King)
           (step w King) (step sw King)
           (step e King) (step ne King)
           (move-type drop-type)
           (drop-piece nx)
       )
  )
  (piece 
       (name Prince)
       (image South "images/migi/sprince.bmp" 
              North "images/migi/nprince.bmp")
       (moves
           (move-type normal-type)
           (step n Rook) (step nw Rook)
           (step s Rook) (step se Rook)
           (step w Rook) (step sw Rook)
           (step e Rook) (step ne Rook)
           (move-type drop-type)
           (drop-piece nx)
       )
  )
  (piece 
       (name Gold)
       (image South "images/migi/sgold.bmp" 
              North "images/migi/ngold.bmp")
       (moves
           (move-type normal-type)
           (step n Lance) (step nw Lance)
           (step s Lance) (step ne Lance)
           (step w Lance) (step e  Lance) 
           (move-type drop-type)
           (drop-piece nx)
       )
  )
  (piece 
       (name Silver)
       (image South "images/migi/ssilver.bmp" 
              North "images/migi/nsilver.bmp")
       (moves
           (move-type normal-type)
           (step nw Knight) (step sw Knight)
           (step se Knight) (step ne Knight)
           (step n  Knight)
           (move-type drop-type)
           (drop-piece nx)
       )
  )
  (piece 
       (name Copper)
       (image South "images/migi/scopper.bmp" 
              North "images/migi/ncopper.bmp")
       (moves
           (move-type normal-type)
           (step n Pawn) (step nw Pawn)
           (step s Pawn) (step ne Pawn)
           (move-type drop-type)
           (drop-piece nx)
       )
  )
  (piece 
       (name Knight)
       (image South "images/migi/sknight.bmp" 
              North "images/migi/nknight.bmp"
              NS "images/migi/sknight.bmp" 
              NN "images/migi/nknight.bmp")
       (moves
           (move-type normal-type)
           (jump n nw Silver)
           (jump n ne Silver)
           (move-type drop-type)
           (drop-piece nx)
       )
  )
  (piece 
       (name Lance)
       (image South "images/migi/slance.bmp" 
              North "images/migi/nlance.bmp"
              NS "images/migi/slance.bmp" 
              NN "images/migi/nlance.bmp")
       (moves
           (move-type normal-type)
           (slide n Gold)
           (move-type drop-type)
           (drop-piece nx)
       )
  )
  (piece 
       (name Rook)
       (image South "images/migi/srook.bmp" 
              North "images/migi/nrook.bmp")
       (moves
           (move-type normal-type)
           (slide n Prince)
           (slide e Prince)
           (slide w Prince)
           (slide s Prince)
           (move-type drop-type)
           (drop-piece nx)
       )
  )
  (piece 
       (name Pawn)
       (image South "images/migi/spawn.bmp" 
              North "images/migi/npawn.bmp"
              NS "images/migi/spawn.bmp" 
              NN "images/migi/npawn.bmp")
       (moves
           (move-type normal-type)
           (step n Copper)
           (move-type drop-type)
           (drop-piece nx)
       )
  )

  (board-setup
        ( South 
            (King   e1)
            (Rook   d1)
            (Gold   c1)
            (Silver b1)
            (Pawn   a1)
        )
        ( North
            (King   a5)
            (Rook   b5)
            (Gold   c5)
            (Silver d5)
            (Pawn   e5)
        )
  )
)
