(define unprom (
  (verify (in-zone? last-rank))
  $1 (verify empty?)
  (add Pawn)
))

(define pawn-shift (
  $1 (verify empty?)
  (verify (not-in-zone? first-rank))
  add
))

(define pawn-jump (
  $1 (verify empty?)
  (verify (in-zone? third-rank))
  $1 (verify empty?)
  add
))

(define step (
  $1 (verify empty?)
  add
))

(define leap (
  $1 $2 (verify empty?)
  add
))

(define slide (
  $1 (while empty? add $1)
))

(define O-O (
  $1 (verify friend?)
  (verify (piece? Rook))
  $1 (verify empty?) to
  $2 cascade $2
  $2 (verify empty?)
  add
))

(define O-O-O (
  $1 (verify friend?)
  (verify (piece? Rook))
  $1 (verify empty?) to
  $2 cascade $2
  $2 (verify empty?)
  $2 (verify empty?)
  add
))

(game
  (title "Retrograde Chess")

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/8x8.bmp")
     (grid
         (start-rectangle 2 2 70 70)
         (dimensions
           ("a/b/c/d/e/f/g/h" (68 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 68)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name last-rank) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name last-rank) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
     (zone (name third-rank) (players White)
           (positions a3 b3 c3 d3 e3 f3 g3 h3)
     )
     (zone (name third-rank) (players Black)
           (positions a6 b6 c6 d6 e6 f6 g6 h6)
     )
     (zone (name first-rank) (players White)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
     (zone (name first-rank) (players Black)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
  )

  (piece
     (name Pawn)
     (image White "images/wpawn.bmp"
            Black "images/bpawn.bmp")
     (moves
         (move-type normal-type)
         (pawn-shift s) (pawn-jump s) 
         (move-type prior-type)
         (pawn-shift sw) (pawn-shift se)
     )
  )
  (piece
     (name Rook)
     (image White "images/wrook.bmp"
            Black "images/brook.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
         (move-type prior-type)
         (unprom sw) (unprom se)
     )
  )
  (piece
     (name Knight)
     (image White "images/wknight.bmp"
            Black "images/bknight.bmp")
     (moves
         (move-type normal-type)
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
         (unprom s)
         (move-type prior-type)
         (unprom sw) (unprom se)
     )
  )
  (piece
     (name Bishop)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (move-type normal-type)
         (slide nw) (slide sw) (slide ne) (slide se)
         (unprom s)
     )
  )
  (piece
     (name Queen)
     (image White "images/wqueen.bmp"
            Black "images/bqueen.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name King)
     (image White "images/wking.bmp"
            Black "images/bking.bmp")
     (moves
         (move-type normal-type)
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
         (O-O w e) (O-O-O e w)
     )
  )
)
