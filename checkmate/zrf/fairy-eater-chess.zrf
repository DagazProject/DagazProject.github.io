(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? promotion-rank)
      (add Ghost Shield Chaos Reaper Knightrider)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? promotion-rank)
      (add Ghost Shield Chaos Reaper Knightrider)
   else
      add
  )
))

(define step (
  $1 (verify not-friend?)
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(define slide-2 (
  $1 $2
  (while empty? add $1 $2)
  (verify not-friend?)
  add
))

(game
  (title "Fairy Eater Chess")

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/fairy-eater/9x9.bmp")
     (grid
         (start-rectangle 17 17 82 82)
         (dimensions
           ("a/b/c/d/e/f/g/h/i" (65 0)) ; files
           ("9/8/7/6/5/4/3/2/1" (0 65)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name promotion-rank) (players White)
           (positions a6 b6 c6 d6 e6 f6 g6 h6 i6)
     )
     (zone (name promotion-rank) (players Black)
           (positions a4 b4 c4 d4 e4 f4 g4 h4 i4)
     )
  )

  (piece
     (name Pawn)
     (image White "images/fairy-eater/wpawn.bmp"
            Black "images/fairy-eater/bpawn.bmp")
     (moves
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
     )
  )
  (piece
     (name Shield)
     (image White "images/fairy-eater/wshield.bmp"
            Black "images/fairy-eater/bshield.bmp")
     (moves
         (step n) (step s)
         (slide w) (slide e)
     )
  )
  (piece
     (name Ghost)
     (image White "images/fairy-eater/wghost.bmp"
            Black "images/fairy-eater/bghost.bmp")
     (moves
         (slide-2 n n) (slide-2 nw nw)
         (slide-2 e e) (slide-2 ne ne)
         (slide-2 w w) (slide-2 sw sw)
         (slide-2 s s) (slide-2 se se)
     )
  )
  (piece
     (name Chaos)
     (image White "images/fairy-eater/wchaos.bmp"
            Black "images/fairy-eater/bchaos.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
         (slide nw) (slide ne) (slide sw) (slide se)
     )
  )
  (piece
     (name Reaper)
     (image White "images/fairy-eater/wreaper.bmp"
            Black "images/fairy-eater/breaper.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
         (slide n) (slide e) (slide w) (slide s)
     )
  )
  (piece
     (name Knightrider)
     (image White "images/fairy-eater/wknightrider.bmp"
            Black "images/fairy-eater/bknightrider.bmp")
     (moves
         (slide-2 n nw) (slide-2 n ne) (slide-2 s sw) (slide-2 s se)
         (slide-2 w nw) (slide-2 w sw) (slide-2 e ne) (slide-2 e se)
     )
  )
  (piece
     (name King)
     (image White "images/fairy-eater/wking.bmp"
            Black "images/fairy-eater/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )

  (board-setup
    (White 
         (Pawn a4 b4 c4 d4 e4 f4 g4 h4 i4)
         (Shield d3 e3 f3 e2)
         (Ghost d1 d2 f1 f2)
         (Chaos a3 b3 c3 g3 h3 i3)
         (Reaper a2 b2 c2 g2 h2 i2)
         (Knightrider a1 b1 c1 g1 h1 i1)
         (King e1)
    )
    (Black 
         (Pawn a6 b6 c6 d6 e6 f6 g6 h6 i6)
         (Shield d7 e7 f7 e8)
         (Ghost d8 d9 f8 f9)
         (Chaos a7 b7 c7 g7 h7 i7)
         (Reaper a8 b8 c8 g8 h8 i8)
         (Knightrider a9 b9 c9 g9 h9 i9)
         (King e9)
    )
  )

  (loss-condition (White Black) (checkmated King))
  (draw-condition (White Black) stalemated)
  (loss-condition (White Black) repetition)
)
