(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? promotion)
      (add Mantri)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? promotion)
      (add Mantri)
   else
      add
  )
))

(define step (
  $1 (verify not-friend?)
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(game
  (title "Chaturanga")
  (history "https://en.wikipedia.org/wiki/Chaturanga")

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (option "animate captures"     false)
  (option "smart moves"          true)
; (option "chaturanga-promotion" true)

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/ahtapada.bmp")
     (grid
         (start-rectangle 2 2 52 52)
         (dimensions
           ("a/b/c/d/e/f/g/h" (50 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 50)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name promotion) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name promotion) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
     (zone (name rook-column) (players White)
           (positions a8 h8)
     )
     (zone (name rook-column) (players Black)
           (positions a1 h1)
     )
     (zone (name knight-column) (players White)
           (positions b8 g8)
     )
     (zone (name knight-column) (players Black)
           (positions b1 g1)
     )
     (zone (name bishop-column) (players White)
           (positions c8 f8)
     )
     (zone (name bishop-column) (players Black)
           (positions c1 f1)
     )
     (zone (name queen-column) (players White)
           (positions d8 e8)
     )
     (zone (name queen-column) (players Black)
           (positions d1 e1)
     )
  )

  (piece
     (name Raja)
     (image White "images/wking.bmp"
            Black "images/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Mantri)
     (image White "images/wqueen.bmp"
            Black "images/bqueen.bmp")
     (moves
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Ratha)
     (image White "images/wrook.bmp"
            Black "images/brook.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Gaja)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (leap nw nw) (leap ne ne) (leap sw sw) (leap se se)
     )
  )
  (piece
     (name Ashva)
     (image White "images/wknight.bmp"
            Black "images/bknight.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Bhata)
     (image White "images/wpawn.bmp"
            Black "images/bpawn.bmp")
     (moves
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
     )
  )

  (board-setup
    (White 
         (Bhata a2 b2 c2 d2 e2 f2 g2 h2)
         (Ratha a1 h1)
         (Ashva b1 g1)
         (Gaja c1 f1)
         (Mantri d1)
         (Raja e1)
    )
    (Black 
         (Bhata a7 b7 c7 d7 e7 f7 g7 h7)
         (Ratha a8 h8)
         (Ashva b8 g8)
         (Gaja c8 f8)
         (Mantri e8)
         (Raja d8)
    )
  )

  (loss-condition (White Black) (pieces-remaining 0 Raja))
  (loss-condition (White Black) (pieces-remaining 1))
  (loss-condition (White Black) repetition)
  (win-condition  (White Black) stalemated)
)

(variant
  (title "Chaturanga (variant)")
  (piece
     (name Gaja)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (step nw) (step sw) (step ne) (step se)
         (step n)
     )
  )
)

(variant
  (title "Chaturanga (variant)")
  (piece
     (name Gaja)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (leap n n) (leap e e) (leap w w) (leap s s)
     )
  )
)
