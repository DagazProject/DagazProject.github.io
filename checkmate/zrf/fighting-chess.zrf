(define capture-piece (
  capture add
))

(define pawn-shift (
  $1 (verify not-friend?)
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define pawn-jump (
  $1
  (verify empty?)
  (verify (in-zone? third-rank))
  $1 (verify empty?)
  add
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define en-passant (
  $1
  (verify enemy?)
  (verify last-to?)
  (verify (piece? Pawn))
  capture
  n mark n
  (verify last-from?)
  back
  add
))

(define step (
  $1 (verify not-friend?)
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(define O-O (
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify friend?) (verify (piece? Rook))
  cascade
  $2 $2
  add
))

(define O-O-O (
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify empty?)
  $1 (verify friend?) (verify (piece? Rook))
  cascade
  $2 $2 $2
  add
))

(game
  (title "Fighting Chess")

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/8x8.bmp")
     (positions
         (W 4 4 20 20)
         (B 528 528 544 544)
     )
     (grid
         (start-rectangle 2 2 70 70)
         (dimensions
           ("a/b/c/d/e/f/g/h" (68 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 68)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name last-rank) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name last-rank) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
     (zone (name third-rank) (players White)
           (positions a3 b3 c3 d3 e3 f3 g3 h3)
     )
     (zone (name third-rank) (players Black)
           (positions a6 b6 c6 d6 e6 f6 g6 h6)
     )
  )

  (piece
     (name Pawn)
     (image White "images/wpawn.bmp"
            Black "images/bpawn.bmp")
     (moves
         (pawn-shift n) (pawn-jump n) 
         (pawn-step nw) (pawn-step ne)
         (en-passant e) (en-passant w)
     )
  )
  (piece
     (name Rook)
     (image White "images/wrook.bmp"
            Black "images/brook.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Knight)
     (image White "images/wknight.bmp"
            Black "images/bknight.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Bishop)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (slide nw) (slide sw) (slide ne) (slide se)
         (step n) (step s) (step w) (step e)
     )
  )
  (piece
     (name Queen)
     (image White "images/wqueen.bmp"
            Black "images/bqueen.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name King)
     (image White "images/wking.bmp"
            Black "images/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
         (O-O e w) (O-O-O w e)
     )
  )
  (piece
     (name Pass)
     (image White "images/p.bmp"
            Black "images/p.bmp")
     (moves
         (capture-piece)
     )
  )

  (board-setup
    (White 
         (Pawn a2 b2 c2 d2 e2 f2 g2 h2)
         (Rook a1 h1)
         (Knight b1 g1)
         (Bishop c1 f1)
         (Queen d1)
         (King e1)
         (Pass W)
    )
    (Black 
         (Pawn a7 b7 c7 d7 e7 f7 g7 h7)
         (Rook a8 h8)
         (Knight b8 g8)
         (Bishop c8 f8)
         (Queen d8)
         (King e8)
    )
  )
)
