(define check-hidden
  (verify (not-piece? UnknownPawn))
  (verify (not-piece? UnknownShield))
  (verify (not-piece? UnknownGhost))
  (verify (not-piece? UnknownChaos))
  (verify (not-piece? UnknownReaper))
  (verify (not-piece? UnknownKnightrider))
  (verify (not-piece? UnknownKing))
)

(define check-friend
  (if friend?
      (verify (not-piece? Pawn))
      (verify (not-piece? Shield))
      (verify (not-piece? Ghost))
      (verify (not-piece? Chaos))
      (verify (not-piece? Reaper))
      (verify (not-piece? Knightrider))
      (verify (not-piece? King))
  )
)

(define change-to (
  (add $1)
))

(define pawn-change-to (
  (if (in-zone? promotion-rank)
      (add Ghost)
   else
      (add $1)
  )
))

(define pawn-shift (
  (verify friend?)
  $1 (verify empty?)
  (if (in-zone? promotion-rank)
      (add Ghost)
   else
      add
  )
))

(define pawn-step (
  (verify friend?)
  $1 (verify enemy?)
  (check-hidden)
  (if (in-zone? promotion-rank)
      (add Ghost)
   else
      add
  )
))

(define king-step (
  (verify friend?)
  $1 (check-friend)
  add
))

(define step (
  (verify friend?)
  $1 (verify not-friend?)
  (check-hidden)
  add
))

(define leap (
  (verify friend?)
  $1 $2 (verify not-friend?)
  (check-hidden)
  add
))

(define slide (
  (verify friend?)
  $1 (while empty? add $1)
  (verify not-friend?)
  (check-hidden)
  add
))

(define slide-2 (
  (verify friend?)
  $1 $2 (while empty? add $1 $2)
  (verify not-friend?)
  (check-hidden)
  add
))

(game
  (title "Esoteric Fairy Eater")

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/fairy-eater/9x9.bmp")
     (grid
         (start-rectangle 17 17 82 82)
         (dimensions
           ("a/b/c/d/e/f/g/h/i" (65 0)) ; files
           ("9/8/7/6/5/4/3/2/1" (0 65)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name promotion-rank) (players White)
           (positions a6 b6 c6 d6 e6 f6 g6 h6 i6
                      a7 b7 c7 d7 e7 f7 g7 h7 i7
                      a8 b8 c8 d8 e8 f8 g8 h8 i8
                      a9 b9 c9 d9 e9 f9 g9 h9 i9
           )
     )
     (zone (name promotion-rank) (players Black)
           (positions a4 b4 c4 d4 e4 f4 g4 h4 i4
                      a3 b3 c3 d3 e3 f3 g3 h3 i3
                      a2 b2 c2 d2 e2 f2 g2 h2 i2
                      a1 b1 c1 d1 e1 f1 g1 h1 i1
           )
     )
     (zone (name home-zone) (players White Black)
           (positions a6 b6 c6 d6 e6 f6 g6 h6 i6
                      a7 b7 c7 d7 e7 f7 g7 h7 i7
                      a8 b8 c8 d8 e8 f8 g8 h8 i8
                      a9 b9 c9 d9 e9 f9 g9 h9 i9
                      a4 b4 c4 d4 e4 f4 g4 h4 i4
                      a3 b3 c3 d3 e3 f3 g3 h3 i3
                      a2 b2 c2 d2 e2 f2 g2 h2 i2
                      a1 b1 c1 d1 e1 f1 g1 h1 i1
           )
     )
  )

  (piece
     (name Pawn)
     (image White "images/fairy-eater/wpawn.bmp"
            Black "images/fairy-eater/bpawn.bmp")
     (moves
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
     )
  )
  (piece
     (name UnknownPawn)
     (image White "images/fairy-eater/unknown.bmp"
            Black "images/fairy-eater/unknown.bmp")
     (moves
         (pawn-change-to Pawn)
     )
  )
  (piece
     (name Shield)
     (image White "images/fairy-eater/wshield.bmp"
            Black "images/fairy-eater/bshield.bmp")
     (moves
         (step n) (step s)
         (slide w) (slide e)
     )
  )
  (piece
     (name UnknownShield)
     (image White "images/fairy-eater/unknown.bmp"
            Black "images/fairy-eater/unknown.bmp")
     (moves
         (change-to Shield)
     )
  )
  (piece
     (name Ghost)
     (image White "images/fairy-eater/wghost.bmp"
            Black "images/fairy-eater/bghost.bmp")
     (moves
         (slide-2 n n) (slide-2 nw nw)
         (slide-2 e e) (slide-2 ne ne)
         (slide-2 w w) (slide-2 sw sw)
         (slide-2 s s) (slide-2 se se)
     )
  )
  (piece
     (name UnknownGhost)
     (image White "images/fairy-eater/unknown.bmp"
            Black "images/fairy-eater/unknown.bmp")
     (moves
         (change-to Ghost)
     )
  )
  (piece
     (name Chaos)
     (image White "images/fairy-eater/wchaos.bmp"
            Black "images/fairy-eater/bchaos.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
         (slide nw) (slide ne) (slide sw) (slide se)
     )
  )
  (piece
     (name UnknownChaos)
     (image White "images/fairy-eater/unknown.bmp"
            Black "images/fairy-eater/unknown.bmp")
     (moves
         (change-to Chaos)
     )
  )
  (piece
     (name Reaper)
     (image White "images/fairy-eater/wreaper.bmp"
            Black "images/fairy-eater/breaper.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
         (slide n) (slide e) (slide w) (slide s)
     )
  )
  (piece
     (name UnknownReaper)
     (image White "images/fairy-eater/unknown.bmp"
            Black "images/fairy-eater/unknown.bmp")
     (moves
         (change-to Reaper)
     )
  )
  (piece
     (name Knightrider)
     (image White "images/fairy-eater/wknightrider.bmp"
            Black "images/fairy-eater/bknightrider.bmp")
     (moves
         (slide-2 n nw) (slide-2 n ne) (slide-2 s sw) (slide-2 s se)
         (slide-2 w nw) (slide-2 w sw) (slide-2 e ne) (slide-2 e se)
     )
  )
  (piece
     (name UnknownKnightrider)
     (image White "images/fairy-eater/unknown.bmp"
            Black "images/fairy-eater/unknown.bmp")
     (moves
         (change-to Knightrider)
     )
  )
  (piece
     (name King)
     (image White "images/fairy-eater/wking.bmp"
            Black "images/fairy-eater/bking.bmp")
     (moves
         (king-step n) (king-step s) (king-step w) (king-step e)
         (king-step nw) (king-step sw) (king-step ne) (king-step se)
     )
  )
  (piece
     (name UnknownKing)
     (image White "images/fairy-eater/unknown.bmp"
            Black "images/fairy-eater/unknown.bmp")
     (moves
         (change-to King)
     )
  )

  (board-setup
    (White 
         (UnknownPawn off 9)
         (UnknownShield off 4)
         (UnknownGhost off 4)
         (UnknownChaos off 6)
         (UnknownReaper off 6)
         (UnknownKnightrider off 6)
         (UnknownKing off 1)
    )
    (Black 
         (UnknownPawn off 9)
         (UnknownShield off 4)
         (UnknownGhost off 4)
         (UnknownChaos off 6)
         (UnknownReaper off 6)
         (UnknownKnightrider off 6)
         (UnknownKing off 1)
    )
  )
)
