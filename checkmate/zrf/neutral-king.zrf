(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? promotion)
      (add Rook)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? promotion)
      (add Rook)
   else
      add
  )
))

(define king-step (
  $1 add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(game
  (title "Neutral King")

  (option "animate captures"  false)
  (option "smart moves"       false)

  (players White Black Neutral)
  (turn-order White Black)

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (board
     (image "images/8x8.bmp")
     (grid
         (start-rectangle 2 2 70 70)
         (dimensions
           ("a/b/c/d/e/f/g/h" (68 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 68)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name promotion) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name promotion) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
  )

  (piece
     (name Pawn)
     (image White "images/wpawn.bmp"
            Black "images/shabel/bpawn.bmp")
     (moves
         (move-type normal-type)
         (pawn-shift n) (pawn-step nw) (pawn-step ne)
     )
  )
  (piece
     (name Rook)
     (image White "images/wrook.bmp"
            Black "images/shabel/brook.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Knight)
     (image White "images/wknight.bmp"
            Black "images/shabel/bknight.bmp")
     (moves
         (move-type normal-type)
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Bishop)
     (image White "images/wbishop.bmp"
            Black "images/shabel/bbishop.bmp")
     (moves
         (move-type normal-type)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name King)
     (image Neutral "images/bking.bmp")
     (moves
         (move-type normal-type)
         (king-step w) (king-step e)
         (move-type white-type)
         (king-step n) (king-step nw) (king-step ne)
         (move-type black-type)
         (king-step n) (king-step nw) (king-step ne)
         (move-type escape-type)
         (king-step s) (king-step sw) (king-step se)
     )
  )

  (board-setup
    (Neutral (King h5))
    (White 
         (Pawn a3 b3 c3 d3 d2 d1)
         (Rook a1 a2)
         (Bishop b1 b2)
         (Knight c1 c2)
    )
    (Black 
         (Pawn a6 b6 c6 d6 d7 d8)
         (Rook a8 a7)
         (Bishop b8 b7)
         (Knight c8 c7)
    )
  )

  (loss-condition (White Black) (checkmated King))
  (draw-condition (White Black) stalemated)
  (loss-condition (White Black) repetition)
)
