(define checker-shift (
   $1 (verify empty?)
   (if (in-zone? promotion)
      (add Dama)
    else
      add
   )
))

(define checker-jump (
   $1 (verify enemy?) 
   capture 
   $1 (verify empty?)
   (if (in-zone? promotion)
      (add Dama)
    else
       (add-partial jump-type)
   )
))

(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? promotion)
      (add Queen)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? promotion)
      (add Queen)
   else
      add
  )
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define step (
  $1 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(game
  (title "Men Row Chess")

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/8x10.bmp")
     (grid
         (start-rectangle 1 2 51 52)
         (dimensions
           ("a/b/c/d/e/f/g/h" (50 0)) ; files
           ("10/9/8/7/6/5/4/3/2/1" (0 50)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name promotion) (players White)
           (positions a10 b10 c10 d10 e10 f10 g10 h10)
     )
     (zone (name promotion) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
  )

  (piece
     (name Man)
     (image White "images/shabel/wman.bmp"
            Black "images/shabel/bman.bmp")
     (moves
         (move-type jump-type)
         (checker-jump nw) (checker-jump ne)
         (move-type normal-type)
         (checker-shift nw) (checker-shift ne)
     )
  )
  (piece
     (name Dama)
     (image White "images/shabel/wdamone.bmp"
            Black "images/shabel/bdamone.bmp")
     (moves
         (move-type jump-type)
         (checker-jump nw) (checker-jump ne) (checker-jump sw) (checker-jump se)
         (move-type normal-type)
         (checker-shift nw) (checker-shift ne) (checker-shift sw) (checker-shift se)
     )
  )
  (piece
     (name Pawn)
     (image White "images/wpawn.bmp"
            Black "images/shabel/bpawn.bmp")
     (moves
         (move-type normal-type)
         (pawn-shift n) (pawn-step nw) (pawn-step ne)
     )
  )
  (piece
     (name Rook)
     (image White "images/wrook.bmp"
            Black "images/shabel/brook.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Knight)
     (image White "images/wknight.bmp"
            Black "images/shabel/bknight.bmp")
     (moves
         (move-type normal-type)
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Bishop)
     (image White "images/wbishop.bmp"
            Black "images/shabel/bbishop.bmp")
     (moves
         (move-type normal-type)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name Queen)
     (image White "images/wqueen.bmp"
            Black "images/shabel/bqueen.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name King)
     (image White "images/wking.bmp"
            Black "images/shabel/bking.bmp")
     (moves
         (move-type normal-type)
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )

  (board-setup
    (White 
         (Man a2 b2 c2 d2 e2 f2 g2 h2)
         (Pawn a3 b3 c3 d3 e3 f3 g3 h3)
         (Rook a1 h1)
         (Knight b1 g1)
         (Bishop c1 f1)
         (Queen d1)
         (King e1)
    )
    (Black 
         (Man a9 b9 c9 d9 e9 f9 g9 h9)
         (Pawn a8 b8 c8 d8 e8 f8 g8 h8)
         (Rook a10 h10)
         (Knight b10 g10)
         (Bishop c10 f10)
         (Queen d10)
         (King e10)
    )
  )
)
