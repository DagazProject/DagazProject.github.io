(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define step (
  $1 (verify not-friend?)
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(define pq-shift (
  (create Queen)
  $1 (verify empty?)
  (add Pawn)
))

(define pq-step (
  (create Queen)
  $1 (verify enemy?)
  (add Pawn)
))

(game
  (title "Short Assize")

  (option "animate captures"  false)
  (option "smart moves"       true)
  (option "chess invariant"   true)

  (players White Black)
  (turn-order White Black)

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (board
     (image "images/8x8.bmp")
     (grid
         (start-rectangle 2 2 70 70)
         (dimensions
           ("a/b/c/d/e/f/g/h" (68 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 68)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name last-rank) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name last-rank) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
     (zone (name home) (players White)
           (positions a1 b1 c1 d1 e1 f1 g1 h1 a2 b2 c2 d2 e2 f2 g2 h2)
     )
     (zone (name home) (players Black)
           (positions a8 b8 c8 d8 e8 f8 g8 h8 a7 b7 c7 d7 e7 f7 g7 h7)
     )
     (zone (name black) (players White Black)
           (positions a1 c1 e1 g1 b2 d2 f2 h2 a3 c3 e3 g3 b4 d4 f4 h4 a5 c5 e5 g5 b6 d6 f6 h6 a7 c7 e7 g7 b8 d8 f8 h8)
     )
  )

  (piece
     (name Pawn)
     (image White "images/wpawn.bmp"
            Black "images/bpawn.bmp")
     (moves
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
     )
  )
  (piece
     (name Rook)
     (image White "images/wrook.bmp"
            Black "images/brook.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Knight)
     (image White "images/wknight.bmp"
            Black "images/bknight.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Bishop)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name Queen)
     (image White "images/wqueen.bmp"
            Black "images/bqueen.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name King)
     (image White "images/wking.bmp"
            Black "images/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name PQ)
     (image White "images/wpq.bmp"
            Black "images/bpq.bmp")
     (moves
         (pq-shift n)
         (pq-step nw) (pq-step ne)
     )
  )

  (board-setup
    (White 
         (Pawn a3 b3 c3 d3 f3 g3 h3)
         (PQ e3)
         (Rook b1 g1)
         (Knight d2 e2)
         (Bishop c1 f1)
         (King e1)
    )
    (Black 
         (Pawn a6 b6 c6 d6 e6 f6 g6 h6)
         (PQ e6)
         (Rook b8 g8)
         (Knight d7 e7)
         (Bishop c8 f8)
         (King e8)
    )
  )

  (loss-condition (White Black) (checkmated King))
  (draw-condition (White Black) stalemated)
  (loss-condition (White Black) repetition)
)

(variant
  (title "Short Assize")

  (board-setup
    (White 
         (Pawn a3 b3 c3 d3 f3 g3 h3)
         (PQ e3)
         (Rook c2 f2)
         (Knight d2 e2)
         (Bishop c1 f1)
         (King d1)
    )
    (Black 
         (Pawn a6 b6 c6 d6 e6 f6 g6 h6)
         (PQ e6)
         (Rook c7 f7)
         (Knight d7 e7)
         (Bishop c8 f8)
         (King d8)
    )
  )
)
