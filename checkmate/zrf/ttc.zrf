(define from-reserve (
  (verify (in-zone? reserve-rank)) $1 
  (while (on-board? $1)
     (if friend? add) $1
  )
  (verify friend?)
  add
))

(define promote-pawn (
  (verify (in-zone? last-rank)) a8
  (while (on-board? $1)
     (if not-empty? add) $1
  )
  (verify not-empty?)
  add
))

(define pawn-shift (
  $1 
  (if (in-zone? third-rank)
      (verify not-enemy?) add 
      (verify empty?) $1 
  )
  (verify not-enemy?) add
))

(define pawn-step (
  $1 (verify enemy?)
  add
))

(define en-passant (
  $1
  (verify enemy?)
  (verify last-to?)
  (verify (piece? Pawn))
  capture
  n mark n
  (verify last-from?)
  back
  add
))

(define step (
  $1 add
))

(define leap (
  $1 $2 add
))

(define slide (
  $1 
  (while empty? add $1)
  add
))

(define O-O (
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify friend?) (verify (piece? Rook))
  cascade
  $2 $2
  add
))

(define O-O-O (
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify empty?)
  $1 (verify friend?) (verify (piece? Rook))
  cascade
  $2 $2 $2
  add
))

(game
  (title "Division Tower Chess")

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/ttc/board.bmp")
     (grid
         (start-rectangle 25 99 85 159)
         (dimensions
           ("a/b/c/d/e/f/g/h" (60 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 60)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
              (nx 1 0)
         )
     )
     (grid
         (start-rectangle 25 4 85 64)
         (dimensions
           ("a/b/c/d/e/f/g/h" (60 0)) ; files
           ("9/0" (0 610)) ; ranks
         )
     )
     (links wr
         (a8 a0) (a0 b0) (b0 c0) (c0 d0) (d0 e0) (e0 f0) (f0 g0) (g0 h0)
     )
     (links br
         (a8 a9) (a9 b9) (b9 c9) (c9 d9) (d9 e9) (e9 f9) (f9 g9) (g9 h9)
     )
     (links nx
         (a0 a8) (b0 a8) (c0 a8) (d0 a8) (e0 a8) (f0 a8) (g0 a8) (h0 a8)
         (a9 a8) (b9 a8) (c9 a8) (d9 a8) (e9 a8) (f9 a8) (g9 a8) (h9 a8)
         (h8 a7) (h7 a6) (h6 a5) (h5 a4) (h4 a3) (h3 a2) (h2 a1)
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e) (wr br) (br wr) (nx nx))
     (zone (name last-rank) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name last-rank) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
     (zone (name third-rank) (players White)
           (positions a3 b3 c3 d3 e3 f3 g3 h3)
     )
     (zone (name third-rank) (players Black)
           (positions a6 b6 c6 d6 e6 f6 g6 h6)
     )
     (zone (name reserve-rank) (players White)
           (positions a0 b0 c0 d0 e0 f0 g0 h0)
     )
     (zone (name reserve-rank) (players Black)
           (positions a9 b9 c9 d9 e9 f9 g9 h9)
     )
  )

  (piece
     (name Pawn)
     (image White "images/ttc/wpawn.bmp"
            Black "images/ttc/bpawn.bmp")
     (moves
         (move-type normal-type)
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
         (en-passant e) (en-passant w)
         (move-type reserve-type)
         (from-reserve nx)
         (move-type promote-type)
         (promote-pawn nx)
     )
  )
  (piece
     (name Knight)
     (image White "images/ttc/wknight.bmp"
            Black "images/ttc/bknight.bmp")
     (moves
         (move-type normal-type)
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
         (move-type reserve-type)
         (from-reserve nx)
     )
  )
  (piece
     (name Bishop)
     (image White "images/ttc/wbishop.bmp"
            Black "images/ttc/bbishop.bmp")
     (moves
         (move-type normal-type)
         (slide nw) (slide sw) (slide ne) (slide se)
         (move-type reserve-type)
         (from-reserve nx)
     )
  )
  (piece
     (name Rook)
     (image White "images/ttc/wrook.bmp"
            Black "images/ttc/brook.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
         (move-type reserve-type)
         (from-reserve nx)
     )
  )
  (piece
     (name Queen)
     (image White "images/ttc/wqueen.bmp"
            Black "images/ttc/bqueen.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide sw) (slide ne) (slide se)
         (move-type reserve-type)
         (from-reserve nx)
     )
  )
  (piece
     (name King)
     (image White "images/ttc/wking.bmp"
            Black "images/ttc/bking.bmp")
     (moves
         (move-type normal-type)
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
         (move-type king-type)
         (O-O e w) (O-O-O w e)
     )
  )
  (piece
     (name PawnP)
     (image White "images/ttc/wpawnp.bmp"
            Black "images/ttc/bpawnp.bmp")
     (moves
         (move-type normal-type)
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
         (move-type promote-type)
         (promote-pawn nx)
     )
  )
  (piece
     (name KnightP)
     (image White "images/ttc/wknightp.bmp"
            Black "images/ttc/bknightp.bmp")
     (moves
         (move-type normal-type)
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name BishopP)
     (image White "images/ttc/wbishopp.bmp"
            Black "images/ttc/bbishopp.bmp")
     (moves
         (move-type normal-type)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name RookP)
     (image White "images/ttc/wrookp.bmp"
            Black "images/ttc/brookp.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name QueenP)
     (image White "images/ttc/wqueenp.bmp"
            Black "images/ttc/bqueenp.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name KingP)
     (image White "images/ttc/wkingp.bmp"
            Black "images/ttc/bkingp.bmp")
     (moves
         (move-type normal-type)
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )

  (board-setup
    (White 
         (Pawn a2 b2 c2 d2 e2 f2 g2 h2)
         (Rook a1 h1)
         (Knight b1 g1)
         (Bishop c1 f1)
         (Queen d1)
         (King e1)
    )
    (Black 
         (Pawn a7 b7 c7 d7 e7 f7 g7 h7)
         (Rook a8 h8)
         (Knight b8 g8)
         (Bishop c8 f8)
         (Queen d8)
         (King e8)
    )
  )
)
