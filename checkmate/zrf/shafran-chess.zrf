(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define pawn-jump-2 (
  $1 (verify empty?)
  (verify (in-zone? third-rank))
  $1 (verify empty?)
  add
))

(define pawn-jump-3 (
  $1 (verify empty?)
  (verify (in-zone? third-rank))
  $1 (verify empty?)
  (verify (in-zone? fourth-rank))
  $1 (verify empty?)
  add
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define en-passant-2 (
  $1
  (verify enemy?)
  (verify last-to?)
  (verify (piece? Pawn))
  capture
  n mark n
  (verify last-from?)
  back
  add
))

(define en-passant-3 (
  $1
  (verify enemy?)
  (verify last-to?)
  (verify (piece? Pawn))
  capture
  n mark n n
  (verify last-from?)
  back
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define step (
  $1 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(define O-O (
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify empty?)
  $1 (verify friend?) (verify (piece? Rook))
  cascade
  $2 $2 $2
  add
))

(define O-O-O (
  $1 (verify empty?)
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify friend?) (verify (piece? Rook))
  cascade
  $2 $2
  add
))

(game
  (title "Shafran's Chess")

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/hex/shafran.bmp")
     (grid
         (start-rectangle 48 -120 97 -71)
         (dimensions
           ("a/b/c/d/e/f/g/h/i" (60 35)) ; files
           ("10/9/8/7/6/5/4/3/2/1" (0 70)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (w -2 1) (e 2 -1)
              (nw -1 0) (se 1 0) (ne 1 -1) (sw -1 1)
              (nnw -1 -1) (sse 1 1) (nne 1 -2) (ssw -1 2)
         )
     )
     (symmetry Black (n s) (s n) (e w) (w e) (nw se) (sw ne) (ne sw) (se nw) (nnw sse) (sse nnw) (nne ssw) (ssw nne))
     (kill-positions a10 b10 c10 d10 a9 b9 c9 a8 b8 a7 f1 g1 h1 i1 g2 h2 i2 h3 i3 i4)
     (zone (name last-rank) (players White)
           (positions a6 b7 c8 d9 e10 f10 g10 h10 i10)
     )
     (zone (name last-rank) (players Black)
           (positions a1 b1 c1 d1 e1 f2 g3 h4 i5)
     )
     (zone (name third-rank) (players White)
           (positions b3 c3 d3 e3 f4 g5 h6)
     )
     (zone (name third-rank) (players Black)
           (positions b5 c6 d7 e8 f8 g8 h8)
     )
     (zone (name fourth-rank) (players White)
           (positions d4 e4 f5)
     )
     (zone (name fourth-rank) (players Black)
           (positions d6 e7 f7)
     )
  )

  (piece
     (name Pawn)
     (image White "images/wpawn.bmp"
            Black "images/bpawn.bmp")
     (moves
         (pawn-shift n) (pawn-jump-2 n) (pawn-jump-3 n)
         (pawn-step nnw) (pawn-step nne)
         (en-passant-2 ne) (en-passant-2 nw)
         (en-passant-3 ne) (en-passant-3 nw)
     )
  )
  (piece
     (name Rook)
     (image White "images/wrook.bmp"
            Black "images/brook.bmp")
     (moves
         (slide n) (slide s) 
         (slide nw) (slide ne)
         (slide sw) (slide se)
     )
  )
  (piece
     (name Knight)
     (image White "images/wknight.bmp"
            Black "images/bknight.bmp")
     (moves
         (leap e ne) (leap e se) (leap w nw) (leap w sw) 
         (leap nnw n) (leap nnw nw) (leap nne n) (leap nne ne) 
         (leap ssw s) (leap ssw sw) (leap sse s) (leap sse se) 
     )
  )
  (piece
     (name Bishop)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (slide nnw) (slide ssw) (slide nne) (slide sse)
         (slide e) (slide w) 
     )
  )
  (piece
     (name Queen)
     (image White "images/wqueen.bmp"
            Black "images/bqueen.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide se) (slide sw) (slide ne)
         (slide nnw) (slide sse) (slide ssw) (slide nne)
     )
  )
  (piece
     (name King)
     (image White "images/wking.bmp"
            Black "images/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step se) (step sw) (step ne)
         (step nnw) (step sse) (step ssw) (step nne)
         (O-O ne sw) (O-O nw se)
         (O-O-O ne sw) (O-O-O nw se)
     )
  )

  (board-setup
    (White 
         (Pawn a2 b2 c2 d2 e2 f3 g4 h5 i6)
         (Rook a1 i5)
         (Knight b1 g3)
         (Bishop c1 f2 h4)
         (Queen d1)
         (King e1)
    )
    (Black 
         (Pawn a5 b6 c7 d8 e9 f9 g9 h9 i9)
         (Rook a6 i10)
         (Knight c8 h10)
         (Bishop b7 d9 g10)
         (Queen f10)
         (King e10)
    )
  )

  (loss-condition (White Black) (checkmated King))
  (draw-condition (White Black) stalemated)
  (loss-condition (White Black) repetition)
)
