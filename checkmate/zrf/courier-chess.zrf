(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? last-rank)
      (add Queen)
   else
      add
  )
))

(define step (
  $1 (verify not-friend?)
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(game
  (title "Courier Chess")

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (option "animate captures"  false)
  (option "smart moves"       true)

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/courier.bmp")
     (grid
         (start-rectangle 2 2 52 52)
         (dimensions
           ("a/b/c/d/e/f/g/h/i/j/k/l" (50 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 50)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name last-rank) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8 i8 j8 k8 l8)
     )
     (zone (name last-rank) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1 i1 j1 k1 l1)
     )
  )

  (piece
     (name Pawn)
     (image White "images/wpawn.bmp"
            Black "images/bpawn.bmp")
     (moves
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
     )
  )
  (piece
     (name Rook)
     (image White "images/wrook.bmp"
            Black "images/brook.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Knight)
     (image White "images/wknight.bmp"
            Black "images/bknight.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Bishop)
     (image White "images/welephant.bmp"
            Black "images/belephant.bmp")
     (moves
         (leap nw nw) (leap ne ne) (leap sw sw) (leap se se)
     )
  )
  (piece
     (name Courier)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name Queen)
     (image White "images/wduke.bmp"
            Black "images/bduke.bmp")
     (moves
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Fool)
     (image White "images/wfool.bmp"
            Black "images/bfool.bmp")
     (moves
         (step n) (step s) (step w) (step e)
     )
  )
  (piece
     (name Man)
     (image White "images/wman.bmp"
            Black "images/bman.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name King)
     (image White "images/wking.bmp"
            Black "images/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )

  (board-setup
     (White
         (Pawn a2 b2 c2 d2 e2 f2 g2 h2 i2 j2 k2 l2)
         (Rook a1 l1)
         (Knight b1 k1)
         (Bishop c1 j1)
         (Courier d1 i1)
         (Man e1)
         (Fool h1)
         (King f1)
         (Queen g1)
     )
     (Black        
         (Pawn a7 b7 c7 d7 e7 f7 g7 h7 i7 j7 k7 l7)
         (Rook a8 l8)
         (Knight b8 k8)
         (Bishop c8 j8)
         (Courier d8 i8)
         (Man e8)
         (Fool h8)
         (King f8)
         (Queen g8)
     )
  )

  (loss-condition (White Black) (checkmated King))
  (loss-condition (White Black) stalemated )
  (loss-condition (White Black) (pieces-remaining 1))
)

(variant
  (title "Courier Chess")
  (board-setup
    (White
         (Pawn a4 b2 c2 d2 e2 f2 g4 h2 i2 j2 k2 l4)
         (Rook a1 l1)
         (Knight b1 k1)
         (Bishop c1 j1)
         (Courier d1 i1)
         (Man e1)
         (Fool h1)
         (King f1)
         (Queen g3)
     )
     (Black        
         (Pawn a5 b7 c7 d7 e7 f7 g5 h7 i7 j7 k7 l5)
         (Rook a8 l8)
         (Knight b8 k8)
         (Bishop c8 j8)
         (Courier d8 i8)
         (Man e8)
         (Fool h8)
         (King f8)
         (Queen g6)
     )
  )
)
