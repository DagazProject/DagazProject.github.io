(define checker-shift (
   $1 (verify empty?)
   (if (in-zone? promotion)
      (add Dama)
    else
      add
   )
))

(define checker-jump (
   $1 (verify enemy?) 
   capture 
   $1 (verify empty?)
   (if (in-zone? promotion)
       (add-partial Dama continue-type)
    else
       (add-partial jump-type)
   )
))

(define dama-shift (
   $1 (while empty?
       add $1
   )
))

(define dama-jump (
   $1 (while empty?  $1)
   (verify enemy?)
   $1 (while empty?
       mark
       (while empty? 
          (opposite $1)
       ) 
       capture
       back
       (add-partial continue-type) $1
   )
))

(define dama-continue (
   $1 (while empty? 
       $1 (verify not-last-from?)
   )
   (verify enemy?) 
   $1 (while empty?
       mark
       (while empty? 
          (opposite $1)
       ) 
       capture
       back
       (add-partial continue-type) $1
   )
))

(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? promotion)
      (add Queen)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? promotion)
      (add Queen)
   else
      add
  )
))

(define step (
  $1 (verify not-friend?)
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(define O-O (
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify friend?) (verify (piece? Rook))
  cascade
  $2 $2
  add
))

(define O-O-O (
  $1 (verify empty?)
  $1 (verify empty?)
  to
  $1 (verify empty?)
  $1 (verify friend?) (verify (piece? Rook))
  cascade
  $2 $2 $2
  add
))

(game
  (title "Shabel")

  (option "animate captures"  false)
  (option "smart moves"       true)
  (option "shabel-invariant"  true)

  (players White Black)
  (turn-order White Black)
  (move-priorities jump-type normal-type)

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (board
     (image "images/8x8.bmp")
     (grid
         (start-rectangle 2 2 70 70)
         (dimensions
           ("a/b/c/d/e/f/g/h" (68 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 68)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name promotion) (players White)
           (positions a8 b8 c8 d8 e8 f8 g8 h8)
     )
     (zone (name promotion) (players Black)
           (positions a1 b1 c1 d1 e1 f1 g1 h1)
     )
  )

  (piece
     (name Man)
     (image White "images/shabel/wman.bmp"
            Black "images/shabel/bman.bmp")
     (moves
         (move-type jump-type)
         (checker-jump nw) (checker-jump ne) (checker-jump sw) (checker-jump se)
         (move-type normal-type)
         (checker-shift nw) (checker-shift ne)
     )
  )
  (piece
     (name Dama)
     (image White "images/shabel/wdamone.bmp"
            Black "images/shabel/bdamone.bmp")
     (moves
         (move-type jump-type)
         (dama-jump nw) (dama-jump ne) (dama-jump sw) (dama-jump se)
         (move-type continue-type)
         (dama-continue nw) (dama-continue ne) (dama-continue sw) (dama-continue se)
         (move-type normal-type)
         (dama-shift nw) (dama-shift ne) (dama-shift sw) (dama-shift se)
     )
  )
  (piece
     (name Pawn)
     (image White "images/wpawn.bmp"
            Black "images/shabel/bpawn.bmp")
     (moves
         (move-type normal-type)
         (pawn-shift n) (pawn-step nw) (pawn-step ne)
     )
  )
  (piece
     (name Rook)
     (image White "images/wrook.bmp"
            Black "images/shabel/brook.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Knight)
     (image White "images/wknight.bmp"
            Black "images/shabel/bknight.bmp")
     (moves
         (move-type normal-type)
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Bishop)
     (image White "images/wbishop.bmp"
            Black "images/shabel/bbishop.bmp")
     (moves
         (move-type normal-type)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name Queen)
     (image White "images/wqueen.bmp"
            Black "images/shabel/bqueen.bmp")
     (moves
         (move-type normal-type)
         (slide n) (slide s) (slide w) (slide e)
         (slide nw) (slide sw) (slide ne) (slide se)
     )
  )
  (piece
     (name King)
     (image White "images/wking.bmp"
            Black "images/shabel/bking.bmp")
     (moves
         (move-type normal-type)
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
         (O-O e w) (O-O-O w e)
     )
  )

  (board-setup
    (White 
         (Man a2 b2 c2 d2 e2 f2 g2 h2 a3 b3 c3 d3 e3 f3 g3 h3)
         (Rook a1 h1)
         (Knight b1 g1)
         (Bishop c1 f1)
         (Queen d1)
         (King e1)
    )
    (Black 
         (Man a7 b7 c7 d7 e7 f7 g7 h7 a6 b6 c6 d6 e6 f6 g6 h6)
         (Rook a8 h8)
         (Knight b8 g8)
         (Bishop c8 f8)
         (Queen d8)
         (King e8)
    )
  )

  (loss-condition (White Black) (checkmated King))
  (draw-condition (White Black) stalemated)
  (loss-condition (White Black) repetition)
)

(variant
  (title "Belarusian Chess")

  (board-setup
    (White 
         (Man a1 a3 b2 c1 c3 d2 e3 f2 g1 g3 h2)
         (Pawn a2 b3 c2 d3 e2 f3 g2 h3)
         (Rook h1)
         (Knight b1)
         (Bishop f1)
         (Queen d1)
         (King e1)
    )
    (Black 
         (Man a7 b6 b8 c7 d6 e7 f6 f8 g7 h6 h8)
         (Pawn a6 b7 c6 d7 e6 f7 g6 h7)
         (Rook a8)
         (Knight g8)
         (Bishop c8)
         (Queen d8)
         (King e8)
    )
  )
)

(variant
  (title "Shashmaty")
  (default)

  (board-setup
    (White 
         (Man b1 d1 f1 h1 a2 c2 e2 g2 b3 d3 f3 h3)
         (Pawn a3 c3 e3 g3)
         (Rook a1 g1)
         (Knight b2 h2)
         (Bishop d2 f2)
         (Queen c1)
         (King e1)
    )
    (Black 
         (Man a8 c8 e8 g8 b7 d7 f7 h7 a6 c6 e6 g6)
         (Pawn b6 d6 f6 h6)
         (Rook b8 h8)
         (Knight a7 g7)
         (Bishop c7 e7)
         (Queen f8)
         (King d8)
    )
  )
)
