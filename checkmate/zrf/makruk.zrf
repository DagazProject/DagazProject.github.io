(define pawn-shift (
  $1 (verify empty?)
  (if (in-zone? promotion)
      (add Met)
   else
      add
  )
))

(define pawn-step (
  $1 (verify enemy?)
  (if (in-zone? promotion)
      (add Met)
   else
      add
  )
))

(define step (
  $1 (verify not-friend?)
  add
))

(define leap (
  $1 $2 (verify not-friend?)
  add
))

(define slide (
  $1 
  (while empty? add $1)
  (verify not-friend?)
  add
))

(game
  (title "Makruk")
  (history "https://en.wikipedia.org/wiki/Makruk")

  (move-sound "Audio\Pickup.wav")
  (release-sound "Audio\Pickup.wav")
  (capture-sound "")
  (drop-sound "")

  (option "animate captures"     false)
  (option "smart moves"          true)

  (players White Black)
  (turn-order White Black)

  (board
     (image "images/makruk.bmp")
     (grid
         (start-rectangle 2 2 52 52)
         (dimensions
           ("a/b/c/d/e/f/g/h" (50 0)) ; files
           ("8/7/6/5/4/3/2/1" (0 50)) ; ranks
         )
         (directions 
              (n 0 -1) (s 0 1) (e 1 0) (w -1 0)
              (nw -1 -1) (sw -1 1) (ne 1 -1) (se 1 1)
         )
     )
     (symmetry Black (n s) (s n) (nw sw) (sw nw) (ne se) (se ne) (w w) (e e))
     (zone (name promotion) (players White)
           (positions a6 b6 c6 d6 e6 f6 g6 h6)
     )
     (zone (name promotion) (players Black)
           (positions a3 b3 c3 d3 e3 f3 g3 h3)
     )
  )

  (piece
     (name Khun)
     (image White "images/wking.bmp"
            Black "images/bking.bmp")
     (moves
         (step n) (step s) (step w) (step e)
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Met)
     (image White "images/wqueen.bmp"
            Black "images/bqueen.bmp")
     (moves
         (step nw) (step sw) (step ne) (step se)
     )
  )
  (piece
     (name Ruea)
     (image White "images/wrook.bmp"
            Black "images/brook.bmp")
     (moves
         (slide n) (slide s) (slide w) (slide e)
     )
  )
  (piece
     (name Khon)
     (image White "images/wbishop.bmp"
            Black "images/bbishop.bmp")
     (moves
         (step nw) (step sw) (step ne) (step se)
         (step n)
     )
  )
  (piece
     (name Ma)
     (image White "images/wknight.bmp"
            Black "images/bknight.bmp")
     (moves
         (leap n nw) (leap n ne) (leap s sw) (leap s se)
         (leap w nw) (leap w sw) (leap e ne) (leap e se)
     )
  )
  (piece
     (name Bia)
     (image White "images/wpawn.bmp"
            Black "images/bpawn.bmp")
     (moves
         (pawn-shift n)
         (pawn-step nw) (pawn-step ne)
     )
  )

  (board-setup
    (White 
         (Bia a3 b3 c3 d3 e3 f3 g3 h3)
         (Ruea a1 h1)
         (Ma b1 g1)
         (Khon c1 f1)
         (Met e1)
         (Khun d1)
    )
    (Black 
         (Bia a6 b6 c6 d6 e6 f6 g6 h6)
         (Ruea a8 h8)
         (Ma b8 g8)
         (Khon c8 f8)
         (Met d8)
         (Khun e8)
    )
  )

  (loss-condition (White Black) (checkmated Khun))
  (draw-condition (White Black) stalemated)
  (loss-condition (White Black) repetition)
)
